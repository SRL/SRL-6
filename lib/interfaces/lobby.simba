(*
Lobby
=====

The lobby file holds functions and procedures that are used in the Runescape
lobby.

*)

{$include_once interfaces.simba}
{$include_once gametabs/logout.simba}

{$f-}

(*
type TRSLobby
~~~~~~~~~~~~~

A type that stores functions and properties of the Runescape lobby.
*)
type
  TRSLobby = record(TRSInterface)
    __placeholder: byte;
  end;

(*
var lobby
~~~~~~~~~

Variable that stores functions and properties of the Runescape lobby.
*)
var
  lobby: TRSLobby;

(*
const Lobby Tabs
~~~~~~~~~~~~~~~~

Integer constants that representing the different lobby tabs.

Example:

.. code-block:: pascal

  if (lobby.getTab() = LOBBY_CLAN) then
    writeln('The clan chat tab is open!');

*)
const
  LOBBY_COUNT = 6;
    LOBBY_PLAYER = 0;
    LOBBY_WORLD = 1;
    LOBBY_FRIENDS = 2;
    LOBBY_CHAT = 3;
    LOBBY_CLAN = 4;
    LOBBY_OPTIONS = 5;

{*
TRSLobby.__init()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__init();

Initiates the lobby variable. Called at the end of this file.

.. note::

    by Coh3n
    Last updated: Mar. 3, 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__init();
*}
procedure TRSLobby.__init();
begin
  with self do
  begin
    name := 'RS Lobby';
    ID := ID_INTERFACE_LOBBY;
    parentID := -1;
    static := true;
    x := 0;
    y := 50;
    getClientDimensions(w, h);
  end;
end;

{*
TRSLobby.__skipEmailScreen()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__skipEmailScreen();

Handles the validate email screen that comes up when you enter to lobby.

.. note::

    by NCDS
    Last updated: Mar. 3, 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__skipEmailScreen();
*}
procedure TRSLobby.__skipEmailScreen();
begin
  if (getColor(473, 468) = 1001495) then // gold border around the "confirm" button
  begin
    mouseBox(intToBox(502, 474, 548, 485), MOUSE_LEFT); // the "skip" button
    wait(200 + random(200));
  end;
end;

(*
TRSLobby.getCurrentTab()
~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.getCurrentTab: integer;

Gets current lobby tab.

.. note::

    by Bionicle1800, NCDS

Example:

.. code-block:: pascal

    if (lobby.getCurrentTab() = LOBBY_WORLD) then
      writeln('The world list is open!');

*)
function TRSLobby.getCurrentTab(): integer;
begin
  for result := 0 to (LOBBY_COUNT - 1) do
    if (GetColor(63 + result * 102, 82) = 11582147) then
      exit;

  Result := -1;
end;

(*
TRSLobby.isOpen()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.isOpen(): boolean;

Returns true if we are at the lobby screen is loaded.

.. note::

    by IceFire908 & Bionicle1800

Example:

.. code-block:: pascal

    while (not lobby.isOpen()) do
      wait(100 + Random(400));

*)
function TRSLobby.isOpen(): boolean;
begin
  self.__skipEmailScreen();
  result := inRange(self.getCurrentTab(), LOBBY_PLAYER, LOBBY_OPTIONS);
end;

{*
TRSLobby._open()
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._open(): boolean;

Exits to the lobby screen. Should call players[currentPlayer].exitToLobby()
instead of this function.

.. note::

    by Starblaster100, Raymond, IceFire908, Tarajunky, & Coh3n
    Last Updated: Mar. 3, 2013 by Co3n

Example:

.. code-block:: pascal

    if (players[currentPlayer].exitToLobby()) then
      writeln('We''ve exited to the lobby!');
*}
function TRSLobby._open(): boolean;
var
  t: integer;
begin
  if (lobby.isOpen()) then
  begin
    result := true;
    exit;
  end;

  if (not tabLogout.open()) then
    exit;

  wait(100 + random(200));

  if (tabLogout.clickText(['Lobby'])) then
  begin
    t := (getSystemTime() + (15000));

    while (not result) and (getSystemTime() < t) do
    begin
      print('Waiting for lobby screen...');
      result := lobby.isOpen();
      wait(700 + random(500));
    end;
  end;
end;

{*
TRSLobby._leaveLobby
~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._leaveLobby(): boolean;

Leaves the lobby screen.

.. note::

    by IceFire908
    Last Updated: Mar. 3, 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobby.isOpen()) then
      if (not players[currentPlayer].leaveLobby()) then
        writeln('Oh no, we didn''t make it out of the Lobby Screen.');
*}
function TRSLobby._leaveLobby(): boolean;
var
  t: integer;
  exitButton: TPoint;
begin
  result := (not lobby.isOpen());

  t := (getSystemTime() + 30000);
  exitButton := point(690, 85).rand(3);

  while (not result) and (getSystemTime() < t) do
  begin
    print('Waiting for login screen...');
    mouse(exitButton, MOUSE_LEFT);
    wait(randomRange(1000, 2000));
    result := (not lobby.isOpen());
  end;
end;

begin
  lobby.__init();
end;
