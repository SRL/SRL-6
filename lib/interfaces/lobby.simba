(*
Lobby
=====

The lobby file holds functions and procedures that are used in the Runescape
lobby.

*)

{$include_once gametabs/gametab.simba}

(*
Constant: Lobby
~~~~~~~~~~~~~~~

Integer constants that representing the different lobby tabs.

Example:

.. code-block:: pascal

  if (GetlobbyScreen() = LOBBY_CLAN) then
    writeln('The clan chat tab is open!');

*)
const
  LOBBY_COUNT = 6;
    LOBBY_PLAYER = 0;
    LOBBY_WORLD = 1;
    LOBBY_FRIENDS = 2;
    LOBBY_CHAT = 3;
    LOBBY_CLAN = 4;
    LOBBY_OPTIONS = 5;

(*
getLobbyTab
~~~~~~~~~~~

.. code-block:: pascal

    function getLobbyTab: integer;

Gets current lobby tab.

.. note::

    by Bionicle1800, NCDS

Example:

.. code-block:: pascal

    if (getLobbyTab = LOBBY_WORLD) then
      writeln('The world list is open!');

*)
function getLobbyTab: integer;
begin
  for result := 0 to (LOBBY_COUNT - 1) do
    if (GetColor(63 + result * 102, 82) = 11582147) then
      exit;

  Result := -1;
end;

(*
isLobbyScreenOpen
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function isLobbyScreenOpen: boolean;

Returns true if we are at the lobby screen is loaded.

.. note::

    by IceFire908 & Bionicle1800

Example:

.. code-block:: pascal

    while (not isLobbyScreenOpen) do
      wait(100 + Random(400));

*)
function isLobbyScreenOpen(): boolean;
begin
  result := inRange(getLobbyTab, LOBBY_PLAYER, LOBBY_OPTIONS);
end;

(*
exitToLobby
~~~~~~~~~~~

.. code-block:: pascal

    function exitToLobby(): boolean;

Exits to the lobby screen.

.. note::

    by Starblaster100, Raymond, IceFire908, Tarajunky, & Coh3n
    Last Updated: Jan. 10th, by Co3n

Example:

.. code-block:: pascal

    if (exitToLobby()) then
      writeln('We''ve exited to the lobby!');
*)
function exitToLobby(): boolean;
var
  p: TPoint;
  t := (getSystemTime + (15000));
begin
  addHeader('exitToLobby(): logging out to lobby');

  if (isLobbyScreenOpen()) then
  begin
    closeHeader('exitToLobby(): lobby screen already open');
    result := true;
    exit;
  end;

  if (not gameTab(TAB_LOGOUT)) then
    exit;

  wait(100 + random(200));

  if (findText(p, ['Lobby'], [UpCharsEx], MI_BOX, 1000)) then
  begin
    mouse(p.rand(3, 10), MOUSE_LEFT);

    while (not result) and (getSystemTime < t) do
    begin
      debug('Waiting for lobby screen...');
      result := isLobbyScreenOpen();
      wait(700 + random(500));
    end;
  end;

  closeHeader('exitToLobby(): ' + toStr(result));
end;

(*
leaveLobby
~~~~~~~~~~

.. code-block:: pascal

    function leaveLobby(): boolean;

Leaves the lobby screen.

.. note::

    by IceFire908
    Last Updated: Jan. 7th, 2013 by Coh3n

Example:

.. code-block:: pascal

    if (isLobbyScreenOpen()) then
      if (not leaveLobby()) then
        writeln('Oh no, we didn''t make it out of the Lobby Screen.');
*)
function leaveLobby(): boolean;
var
  t := (getSystemTime() + 30000);
  exitButton := point(690, 85).rand(3);
begin
  addHeader('leaveLobby(): leaving lobby');

  result := (not isLobbyScreenOpen());

  while (not result) and (getSystemTime() < t) do
  begin
    debug('Waiting for login screen...');
    mouse(exitButton, MOUSE_LEFT);
    wait(randomRange(1000, 2000));
    result := (not isLobbyScreenOpen());
  end;

  closeHeader('leaveLobby(): ' + toStr(result));
end;
