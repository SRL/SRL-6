(*
Pinscreen
=========

The pinscreen file holds functions and procedures that are used in the
Runescape pinscreen.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/pinscreen.simba>`_.

*)

{$f-}

{$include_once interfaces.simba}
{$include_once ../core/text.simba}
{$include_once ../interfaces/conversationbox.simba}

(*
type TRSPinScreen
~~~~~~~~~~~~~~~~~

A type that stores all the attributes and functions of the Runescape Pinscreen.

*)
type
  TRSPinScreen = record(TRSInterface)
    __placeHolder: byte;
  end;

(*
var pinScreen
~~~~~~~~~~~~~

The variable that holds all pinscreen information. It used when calling any functions
in this file.

Example:

.. code-block:: pascal

    if (pinscreen.isOpen()) then
      writeln('Pinscreen is open!');

*)
var
  pinScreen: TRSPinScreen;

{*
TRSPinScreen.__init
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSPinScreen.__init();

Initializes the TRSPinScreen.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

    pinScreen.__init();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSPinScreen.__init();
begin
  with (self) do
  begin
    name := 'RS Pin Screen';
    ID := ID_INTERFACE_PINSCREEN;
    parentID := -1;
    static := false;
    setBounds([32, 29, 544, 362]);
  end;
end;
{$ENDIF}

(*
TRSPinScreen.isOpen
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.isOpen(waitTime: integer = 0): boolean;

Returns if the pinscreen is open or not, parameter 'waitTime' is optional and
is default set at 0, increasing will wait x ms for the pinscreen to open.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

   if (pinScreen.isOpen(1000)) then
      writeln('Pinscreen is open!');
*)
function TRSPinScreen.isOpen(waitTime: integer = 0): boolean;
const
  TEXT_COLOR = 13618403;
var
  t: integer;
  p: TPoint;
begin
  t := (getSystemTime() + waitTime);

  repeat
    result := findText(p, TEXT_COLOR, 0, ['Exit'], [UpChars], TBox([self.x2 - 200, self.y2 - 100, self.x2, self.y2]));

    if (result) then
      break();

    if (waitTime > 0) then
      wait(20 + random(20));

  until (getSystemTime() >= t);
end;

(*
TRSPinScreen.close
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.close(): boolean;

Will attempt to close the pinscreen if it's open, returns if succesfull or not.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

   if (pinScreen.close()) then
     writeln('closed the pinscreen!');
*)
function TRSPinScreen.close(): boolean;
begin
  result := false;

  if (self.isOpen()) then
    result := mouseText(['Exit'], [UpChars], self.getBounds(), MOUSE_LEFT);

  print('pinScreen.close(): result = ' + boolToStr(result));
end;

(*
TRSPinScreen.waitNotOpen
~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.waitNotOpen(waitTime: integer): boolean;

Waits 'waitTime' for the pinscreen not to be open.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

   if (pinScreen.waitNotOpen(5000)) then
     writeln('Pinscreen is not open anymore!');
*)
function TRSPinScreen.waitNotOpen(waitTime: integer): boolean;
var
  t: integer;
begin
  t := (getSystemTime() + waitTime);
  result := (not self.isOpen());

  if (result) then
    exit(true);

  while (t > getSystemTime()) and (not result) do
  begin
    result := (not self.isOpen());
    wait(20 + random(30));
  end;
end;

{*
TRSPinScreen.__exit
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.__exit(reason: string; debugType: TDebug; res: boolean): boolean;

Helper function for TRSPinScreen.ener, used to tidy it up.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

    pinScreen.__exit('oops', TDebug.FATAL, false);
*}
{$IFNDEF CODEINSIGHT}
function TRSPinScreen.__exit(reason: string; debugType: TDebug; res: boolean): boolean;
begin
  print(reason, debugType);
  print('pinScreen.enter(): result = ' + toStr(res), TDebug.FOOTER);
  exit(res);
end;

{*
TRSPinScreen.__findNumber
~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.__findNumber(number: char; out b: TBox): boolean;

Attempts to find the number 'number' and will return it's box in 'b'.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

    pinScreen.__findNumber('1', b);
*}
function TRSPinScreen.__findNumber(number: char; out b: TBox): boolean;
var
  numberSlots: TBoxArray;
  area: TBox;
  i: integer;
  p: TPoint;
begin
  result := false;

  if (not self.isOpen()) then
  begin
    print('pinScreen.findNumber(): Unable to find the number because the pinscreen isn''t open', TDebug.ERROR);
    exit();
  end;

  numberSlots := grid(4, 3, 57, 57, 90,72, point(self.x + 64, self.y + 135));
  area := [self.x + 30, self.y + 100, self.x + 370, self.y + self.h - 20];

  // delete the two extra boxes created by grid().
  numberSlots.deleteIndex(7);
  numberSlots.deleteIndex(10);

  if (findText(p, [toStr(number)], [UpCharsEx], area)) then
    for i := 0 to high(numberSlots) do
      if (pointInBox(p, numberSlots[i])) then
      begin
        b := numberSlots[i];
        exit(true);
      end;

  // if we dont find the text in any of the boxes, our mouse must be covering it.
  getMousePos(p.x, p.y);

  for i := 0 to high(numberSlots) do
    if (pointInBox(p, numberSlots[i])) then
    begin
      b := numberSlots[i];
      exit(true);
    end;
end;
{$ENDIF}

(*
TRSPinScreen.isPinVaild
~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.isPinVaild(pin: string): boolean;

Returns if the pin 'pin' is vaild or not.

.. note::

    - by Olly
    - Last Updated: 13 October 2013 by Olly

Example:

.. code-block:: pascal

   if (pinScreen.isPinVaild('1234')) then
      writeln('1234 is a vaild pin!');
*)
function TRSPinScreen.isPinVaild(pin: string): boolean;
begin
  result := (length(extractFromStr(pin, numbers)) = 4);

  if (not result) then
    print('The player''s in isn''t vaild!', TDebug.ERROR);
end;

(*
TRSPinScreen.enter
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSPinScreen.enter(pin: string): boolean;

Attempts to enter the bankpin 'pin', will try twice to enter and will return
if succesfull or not.

.. note::

    - by Olly
    - Last Updated: 22 September 2013 by Olly

Example:

.. code-block:: pascal

   if (pinScreen.enter('1234')) then
      writeln('Entered our bank pin!');
*)
function TRSPinScreen.enter(pin: string): boolean;
var
  i, tries: integer;
  b: TBox;
begin
  result := false;
  print('pinScreen.enter()', TDebug.HEADER);

  if (not self.isPinVaild(pin)) then
  begin
    print('Cannot enter pin because the pin isnt vaild!', TDebug.ERROR);
    print('pinScreen.enter(): result = false', TDebug.FOOTER);
    exit();
  end;

  if (not self.isOpen()) then
  begin
    print('Cannot enter pin because the pinscreen is not open', TDebug.ERROR);
    print('pinScreen.enter(): result = false', TDebug.FOOTER);
    exit();
  end;

  repeat
    inc(tries);

    for i := 1 to 4 do
    begin
      if (self.isOpen()) then
      begin
        if (self.__findNumber(pin[i], b)) then
        begin
          print('Succesfully clicked number ' + pin[i]);
          mouseBox(b, MOUSE_LEFT);

          wait(350 + random(400));
        end else
          exit(self.__exit('Failed to find number ' + pin[i], TDebug.ERROR, false));
      end else
        exit(self.__exit('Pinscreen isn''t open anymore', TDebug.ERROR, false));
    end;

    // now we've entered
    print('Waiting for the outcome of the pin...');
    wait(250 + random(200));

    // wait upto 4 seconds for the pinscreen not to be open
    if (self.waitNotOpen(4000)) then
    begin
      if (not conversationBox.isOpen()) then  // no convo box? = entered pin succesfully
        exit(self.__exit('Succesfully entered pin', TDebug.DEBUG, true));

      if (not conversationBox.findChat(['Please try again', 'ease try aga'])) then // didnt find bad
         exit(self.__exit('Conversation box is open, but didn''t find the text to retry.', TDebug.ERROR, false))
      else
        conversationBox.continue(true, true); // if we did found the text, lets skip it.
    end else
      exit(self.__exit('Pinscreen is still open, something went wrong', TDebug.ERROR, false)); // else pinscreen is still open, something messed up

    print('Going to try and enter our pin again..');
  until (tries >= 2);

  exit(self.__exit('Failed to enter our pin after two tries', TDebug.ERROR, false));
end;


begin
  pinScreen.__init();
end;
