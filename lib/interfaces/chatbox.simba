(*
Chatbox
=======

The chatbox file holds functions and procedures that are used in the
Runescape chat box.

The source for this file can be found
`here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/chatbox.simba>`_.

*)

{$include_once interfaces.simba}
{$include_once actionbar.simba}
{$include_once ../core/text.simba}
{$include_once ../tesseract/tesseract.simba}

{$f-}

(*
type TRSChatbox
~~~~~~~~~~~~~~~

A type that stores functions and properties of the Runescape chat box interface.
*)
type
  TRSChatbox = record(TRSInterface)
    lineBoxes: TBoxArray;
  end;


(*
var chatbox
~~~~~~~~~~~

Variable that stores functions and properties of the Runescape chatbox interface.
*)
var
  chatbox: TRSChatbox;

{*
TRSChatbox.__init
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSChatbox.__init();

Initiates the chatbox variable.

.. note::

    - by Coh3n
    - Last Updated: 08 August 2013 by Coh3n

Example:

.. code-block:: pascal

    chatbox.__init();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSChatbox.__init();
begin
  with self do
  begin
    name := 'RS Chat Box';
    ID := ID_INTERFACE_CHAT_BOX;
    parentID := -1;
    static := false;
  end;
end;
{$ENDIF}

procedure TRSChatbox.__setup();
var
  b, chatBounds: TBox;
  wid, hei, i: integer;
  p: TPoint;
begin
  setLength(self.lineBoxes, 0);
  getClientDimensions(wid, hei);

  self.setBounds([1, actionBar.y2, gameTabs.tabArea.x1, hei-2]);
  setLength(lineBoxes, 0);

  chatBounds := chatBox.getChatArea();

  for i := 0 to 10 do
  begin
    p.y := 568 - (i * 14);
    b := [chatBounds.x1, p.y-9, chatBounds.x2, p.y+9];

    if (b.y1 < chatBox.getChatArea().y1) then
      break();

    insert(b, self.lineBoxes, high(self.lineBoxes)+1);
  end;
end;

function TRSChatbox.getChatArea(): TBox;
begin
  result := intToBox(self.x1+3, self.y1 + 67, self.x2-20, self.y2-21);
end;

(*
TRSChatbox.findAnyText
~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSChatbox.findAnyText(txt: TStringArray): boolean;

Initiates the chatbox variable.

.. note::

    - by Olly
    - Last Updated: 17 October 2013 by Olly

Example:

.. code-block:: pascal

    chatbox.findAnyText(['Hello']);
*)
function TRSChatbox.findAnyText(txt: TStringArray): boolean;
var
  p: TPoint;
begin
  result := findText(p, txt, [smallchars], self.getChatArea());

  if (result) then
    print('TRSChatbox.findAnyText(): Found "'+toStr(txt)+'"', TDebug.SUB);
end;

function TRSChatbox.findTextOnLines(txt: TStringArray; lines: TIntegerArray): boolean;
var
  i: integer;
  p: TPoint;
begin
  for i := 0 to high(lines) do
  begin
    result := findText(p, txt, [SmallChars], self.lineBoxes[lines[i]]);

    if (result) then
    begin
      print('TRSChatBox.findTextOnLines(): Found text '+toStr(txt)+' on line '+intToStr(lines[i]));
      exit();
    end;
  end;
end;

(*
TRSChatbox.getXP
~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSChatbox.getXP(): integer;

Initiates the chatbox variable.

.. note::

    - by Ashaman88
    - Last Updated: 01 January 2013 by Ashaman88

Example:

.. code-block:: pascal

    xp:= chatbox.getXP;
    writeln('Starting with ' + toStr(xp) + ' XP.');
*)
function TRSChatBox.getXP(): integer;
var
  b: TBox;
  s: String;
  tpa : TPointArray;
  atpa : T2DPointArray;
  i,p: Integer;
begin
  b := self.getBounds();
  b.edit(+(b.x2-b.x1)-140, +10, -5, -94);

  findColorsTolerance(tpa, 14013909, b, 4,colorSetting(2, 0.00, 0.00));

  if length(tpa) < 2 then
  begin
    print('chatBox.getXP(): No XP found', TDebug.SUB);
    Exit;
  end;

  atpa := tpa.cluster(5);

  b:= atpa.getbounds();
  b.edit(-2,-2,+2,+3);

  s:=Replace(tesseractgettext(b.x1,b.y1,b.x2,b.y2, FILTER_SMALL_CHARS), ' ', '', [rfReplaceAll]);

  p := Pos('x', S);
  if p > 0 then
    Result := StrToIntDef(ExtractFromStr(Copy(s, p, Length(S)), Numbers), 0)
  else
    Result := StrToIntDef(ExtractFromStr(s, Numbers), 0);

  print('chatBox.getXP(): XP found: ' + tostr(result), TDebug.SUB);
end;

begin
  chatbox.__init();
end;
