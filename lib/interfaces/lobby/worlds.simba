(*
Worlds
======

The worlds file is used to represent the world tab interface in the RS lobby. It
stores the functions needed to switch players between servers.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/lobby/worlds.simba>`_.

*)

{$include_once lobby.simba}

{*
const Internal
~~~~~~~~~~~~~~

Internal constants used throughout several functions.
*}
const
  _WORLD_HIGH                = 139; // the highest number world
  _WORLD_PATH                = includePath + 'SRL-6/lib/interfaces/lobby/worlds.ini';
  _WORLD_FONT                = statChars;
  _WORLD_COLOR_ARROW         = 1074576;  // arrow color that can order the worlds
  _WORLD_COLOR_SCROLL_LEFT   = 3615246;  // blue color on the left side of scroll area
  _WORLD_COLOR_SCROLL_BOTTOM = 3753285;   // black color under scroll bar

(*
const Order
~~~~~~~~~~~

Constants to be used with the TRSLobbyWorlds sorting functions.

Example:

.. code-block:: pascal

    lobbyWorlds.sort(WORLD_ORDER_WORLDS, false);
*)
const
  WORLD_ORDER_WORLDS   = 0;
  WORLD_ORDER_PLAYERS  = 1;
  WORLD_ORDER_ACTIVITY = 2;
  WORLD_ORDER_LOCATION = WORLD_ORDER_ACTIVITY;
  WORLD_ORDER_TYPE     = 3;
  WORLD_ORDER_PING     = 4;

{*
type __TRSWorld
~~~~~~~~~~~~~~~

An internal type used to store individual world information.
*}
type
  __TRSWorld = record
    world: integer;
    members: boolean;
  end;

(*
type TRSLobbyWorlds
~~~~~~~~~~~~~~~~~~~

The type representing the worlds interface in the RS lobby.
*)
type
  TRSLobbyWorlds = record(TRSLobby)
    lobbyID: integer;
    worlds: array of __TRSWorld;
  end;

(*
var lobbyWorlds
~~~~~~~~~~~~~~~

The global variable used to call any worlds interface functions/procedures.

Example:

.. code-block:: pascal

    lobbyWorlds.selectWorld(132);
*)
var
  lobbyWorlds: TRSLobbyWorlds;

{*
var Internal
~~~~~~~~~~~~

Internal worlds interface variables:
*}
var
  __worldsOrdered: boolean; // set to true if the worlds get ordered in the sorting functions

{*
TRSLobbyWorlds.__loadFromINI
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobbyWorlds.__loadFromINI();

Loads the world information from an INI file.

.. note::

    - by Narcle, Nava2, and Coh3n
    - Last Updated: 26 June 2013 by Coh3n

Example:

.. code-block:: pascal

    self.__loadFromINI();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSLobbyWorlds.__loadFromINI();
var
  i, c: integer;
begin
  setLength(self.worlds, _WORLD_HIGH + 1);

  for i := 0 to high(self.worlds) do
    if (readINI(toStr(i), 'world', _WORLD_PATH) <> '') then // to make sure the world exists
    begin
      self.worlds[c].members := strToBool(readINI(toStr(i), 'members', _WORLD_PATH));
      self.worlds[c].world := strToIntDef(readINI(toStr(i), 'world', _WORLD_PATH), -1);
      inc(c);
    end;

  setLength(self.worlds, c);
end;
{$ENDIF}

{*
TRSLobbyWorlds.__init
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobbyWorlds.__init();

Initiates the TRSLobbyWorlds variable.  Called at the end of this file.

.. note::

    - by Coh3n
    - Last Updated: 26 June 2013 by Coh3n

Example:

.. code-block:: pascal

    lobbyWorlds.__init();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSLobbyWorlds.__init();
begin
  with self do
  begin
    name := 'RS Lobby Tab: Worlds';
    ID := ID_INTERFACE_LOBBY_WORLDS;
    parentID := ID_INTERFACE_LOBBY;
    static := true;
    lobbyID := LOBBY_WORLDS;
    self.__loadFromINI();
  end;
end;
{$ENDIF}

{*
TRSLobbyWorlds._getWorldIndex
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds._getWorldIndex(wo: integer): integer;

Retruns the array index the world 'wo' is in.

.. note::

    - by Coh3n
    - Last Updated: 24 July 2013 by Coh3n

Example:

.. code-block:: pascal

    b := lobbyWorlds.worlds[lobbyWorlds._getWorldIndex(12)].member;
*}
function TRSLobbyWorlds._getWorldIndex(wo: integer): integer;
begin
  for result := 0 to high(self.worlds) do
    if (self.worlds[result].world = wo) then
      break;
end;

(*
TRSLobbyWorlds.isOpen
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.isOpen(): boolean;

Returns true if the worlds interface is open.

.. note::

    - by Icefire908 & Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobbyWorlds.isOpen()) then
      lobbyWorlds.selectWorld(115);
*)
function TRSLobbyWorlds.isOpen(): boolean;
begin
  result := (lobby.getCurrentTab() = self.lobbyID);

  // set the world's bounds to the same as the lobby's
  if (result) then
    self.setBounds(lobby.getBounds());
end;

(*
TRSLobbyWorlds.open
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.open(): boolean;

Returns true if the worlds interface is open.  Waits a maximum of 10 seconds
after the tab is clicked to compensate for lag.

.. note::

    - by Bionicle1800 & Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (not lobbyWorlds.isOpen()) then
      lobbyWorlds.open();
*)
function TRSLobbyWorlds.open(): boolean;
var
  t: integer;
  p: TPoint;
begin
  result := lobby.openTab(self.lobbyID);

  if (not self.isOpen()) then
    exit(false);

  // needed to compensate for lag
  if (result) then
  begin
    t := (getSystemTime() + 10000);

    while (getSystemTime() < t) do
    begin
      if (findText(p, ['Free', 'Members'], [statChars], self.getBounds())) then
        exit(true);

      wait(50);
    end;

    print('TRSLobbyWorlds.open(): Worlds list taking to long to load', TDebug.SUB);
  end;
end;

{*
TRSLobbyWorlds._isWorldValid
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds._isWorldValid(wo: integer): boolean;

Returns true if the world (wo) is valid.

.. note::

    - by Coh3n
    - Last Updated: 26 June 2013 by Coh3n

Example:

.. code-block:: pascal

    if (not lobbyWorlds._isWorldValid(115)) then
      exit(false);
*}
function TRSLobbyWorlds._isWorldValid(wo: integer): boolean;
var
  i: integer;
begin
  for i := 0 to high(self.worlds) do
    if (self.worlds[i].world = wo) then
      exit(true);

  print('TRSLobbyWorlds._isWorldValid(): '+toStr(wo)+' is an invalid world', TDebug.ERROR);
end;

(*
TRSLobbyWorlds.getCurrentWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.getCurrentWorld(): integer;

Returns the currently selected world.

.. note::

    - by Home
    - Last Updated: 28 November 2013 by Coh3n

Example:

.. code-block:: pascal

    if (not lobbyWorlds.getCurrentWorld() = 115) then
      lobbyWorlds.selectWorld(115);
*)
function TRSLobbyWorlds.getCurrentWorld(): integer;
var
  n: integer;
  s: string;
begin
  result := -1;

  if (not self.isOpen()) then
    exit();

  s := tesseractGetText(self.x + 550, self.y + 62, self.x+self.w - 15, self.y + 80, FILTER_SMALL_CHARS_2);
  n := strToIntDef(extractFromStr(s, numbers), -1);

  if (self._isWorldValid(n)) then
    result := n;

  print('TRSLobbyWorlds.getCurrentWorld(): Current world is '+toStr(result), TDebug.SUB);
end;

(*
TRSLobbyWorlds.sort
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.sort(by: integer; descending: boolean): boolean;

Sorts the worlds list by the order specified by 'by', and 'descending'.  Setting
'descending' to true will order the worlds with the largest at the top.  Valid
arguements for 'by' are:

    * WORLD_ORDER_WORLDS
    * WORLD_ORDER_PLAYERS
    * WORLD_ORDER_ACTIVITY
    * WORLD_ORDER_LOCATION
    * WORLD_ORDER_TYPE
    * WORLD_ORDER_PING

.. note::

    - by Naum & Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobbyWorlds.sort(WORLD_ORDER_WORLDS, false); // sorts the worlds starting with 1 at the top
*)
function TRSLobbyWorlds.sort(by: integer; descending: boolean): boolean;
var
  ATPA: T2DPointArray;
  TPA: TPointArray;
  TB: TBox;
begin
  print('TRSLobbyWorlds.sort()', TDebug.HEADER);

  if (not self.isOpen()) then
    exit(false);

  // long box around the arrows
  if (findColorsTolerance(TPA, _WORLD_COLOR_ARROW, self.x, self.y + 115, self.x+self.w, self.y + 130, 1)) then
  begin
    print('Found _WORLD_COLOR_ARROW');

    ATPA := splitTPA(TPA, 2);
    sortATPAFromFirstPoint(ATPA, point(self.x, self.y));

    if (length(ATPA) < (by * 2)) then
    begin
      print('Didn''t find enough of _WORLD_COLOR_ARROW', TDebug.ERROR);
      print('TRSLobbyWorlds.sort(): '+toStr(result), TDebug.FOOTER);
      exit();
    end;

    TB := getTPABounds(ATPA[(by * 2) + ord(descending)]);
    mouseBox(intToBox(TB.x1 + 2, TB.y1 + 2, TB.x2, TB.y2), MOUSE_LEFT);

    result := true;
  end else
    print('Failed to find _WORLD_COLOR_ARROW', TDebug.ERROR);

  print('TRSLobbyWorlds.sort(): '+toStr(result), TDebug.FOOTER);
end;

{*
TRSLobbyWorlds._findWorldNumber
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds._findWorldNumber(var res: TPoint; wo: integer): boolean;

Searches for the text of the world 'wo'. Returns true if found. Returns it's
coordinates in 'res'.

.. note::

    - by Coh3n
    - Last Updated: 24 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobbyWorlds._findWorldNumber(p, 115)) then
      mouse(p.rand(5), MOUSE_LEFT);
*}
function TRSLobbyWorlds._findWorldNumber(var res: TPoint; wo: integer): boolean;
var
  i, hi, l, col: integer;
  worldsArea: TBox;
  tpa: TPointArray;
  atpa: T2DPointArray;
  p: TPoint;
begin
  // box is the world number column
  worldsArea := intToBox(self.x + 55, self.y + 115, self.x + 80, self.y+self.h - 50);

  if (self.worlds[self._getWorldIndex(wo)].members) then
    col := 6618364
  else
    col := 16579836;

  findColors(tpa, col, worldsArea);
  atpa := splitTPA(tpa, 10); // separate each row

  l := length(TPAFromText(intToStr(wo), statChars));
  hi := high(atpa);

  // checks TPA length first, then for the text
  for i := 0 to hi do
    if (length(atpa[i]) = l) then
      if (strToIntDef(getTextAtEx(atpa[i].getBounds(), 0, 0, 0, col, 0, statChars, Numbers), -1) = wo) then
      begin
        res := atpa[i].getMiddle();
        result := true;
        break;
      end;

  if (result) then
  begin
    print('TRSLobbyWorlds._findWorldNumber(): Found world '+toStr(wo), TDebug.SUB);
    res := res.rand(400, 10); // add randomness to the point clicked
  end;
end;

{*
TRSLobbyWorlds._scrollToWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds._scrollToWorld(var res: TPoint; wo: integer): boolean;

Uses mouseScroll to find the world 'wo'.  Returns it's corrdinates in 'res'.
Function is internal.  Use TRSLobbyWorlds.selectWorld instead.

.. note::

    - by Coh3n
    - Last Updated: 28 November 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobbyWorlds._scrollToWorld(p, 115)) then
      mouse(p, MOUSE_LEFT);
*}
function TRSLobbyWorlds._scrollToWorld(var res: TPoint; wo: integer): boolean;
const
  __COLOR_SCROLL_BAR = 3455991;
var
  p, tmp: TPoint;
  t: integer;
  down: boolean;
begin
  if (not self.isOpen()) then
    exit(false);

  p := self.getCenterPoint().rand(randomRange(-200, 200), randomRange(-50, 50));
  t := (getSystemTime() + 15000);

  repeat
    if (self._findWorldNumber(res, wo)) then
    begin
      result := true;
      break;
    end;

    if (getColor(self.x2 - 20, self.y1 + 204) = __COLOR_SCROLL_BAR) then
      down := true;

    if (getColor(self.x2 - 20, self.y2 - 71) = __COLOR_SCROLL_BAR) then
      down := false;

    mouseScroll(p, 3 + random(5), down);
    wait(50 + random(50));

  until(getSystemTime() > t);
end;

(*
TRSLobbyWorlds.selectWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.selectWorld(wo: integer): boolean;

Selects the world 'wo'.  Will open the worlds interface and scroll if needed.
Does NOT log the player in.  Will just click the world on the list.

.. note::

    - by Narcle, ZephyrsFury, Ashaman88, and Coh3n
    - Last Updated: 26 June 2013 by Coh3n

Example:

.. code-block:: pascal

    lobbyWorlds.selectWorld(115);
*)
function TRSLobbyWorlds.selectWorld(wo: integer): boolean;
var
  p: TPoint;
begin
  result := false;

  // make sure the worlds screen is open
  if (not self.isOpen()) then
    if (not self.open()) then
      exit(false);

  // make sure the world is valid
  if (not self._isWorldValid(wo)) then
    exit(false);

  print('TRSLobbyWorlds.selectWorld()', TDebug.HEADER);

  // check to see if we're already on the right world
  if (self.getCurrentWorld() = wo) then
  begin
    print('Already on world '+toStr(wo));
    result := true;
  end;

  if (not result) then
  begin
    if (self._findWorldNumber(p, wo)) then // check to see if the worlds already visible (or if it's favorited)
      print('Found world '+toStr(wo))
    else
      if (self._scrollToWorld(p, wo)) then
        print('Scrolled to and found world '+toStr(wo));

    if (not p.isInvalid()) then
    begin
      print('Selected world '+toStr(wo));
      mouse(p, MOUSE_LEFT);
      result := true;
    end;
  end;

  print('TRSLobbyWorlds.selectWorld(): '+toStr(result), TDebug.FOOTER);
end;

(*
TRSLobbyWorlds.selectRandomWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobbyWorlds.selectRandomWorld(members: boolean): boolean;

Selects a random world. P2P or F2P depends on the value of 'members'.

.. note::

    - by Narcle, Mixter, and Coh3n
    - Last Updated: 26 June 2013 by Coh3n

Example:

.. code-block:: pascal

    lobbyWorlds.selectRandomWorld(players[currentPlayer].isMember);
*)
function TRSLobbyWorlds.selectRandomWorld(members: boolean): boolean;
var
  i, l, wo: integer;
  wArray: TIntegerArray;
begin
  setLength(wArray, length(self.worlds));

  for i := 0 to high(self.worlds) do
    if (not self.worlds[i].members = not members) then // the not's are required; don't even ask me why
    begin
      wArray[l] := self.worlds[i].world;
      inc(l);
    end;

  if (l <= 0) then
  begin
    print('TRSLobbyWorlds.selectRandomWorld(): Failed to initiate wArray', TDebug.ERROR);
    exit();
  end;

  setLength(wArray, l);
  wo := random(l);

  print('TRSLobbyWorlds.selectRandomWorld(): Random world is '+toStr(wArray[wo]), TDebug.SUB);

  result := self.selectWorld(wArray[wo]);
end;

begin
  lobbyWorlds.__init();
end;
