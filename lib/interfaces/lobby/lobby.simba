(*
Lobby
=====

The lobby file holds functions and procedures that are used in the Runescape
lobby.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/lobby/lobby.simba>`_.

*)

{$include_once ../interfaces.simba}
{$include_once ../options.simba}
{$include_once ../../tesseract/tesseract.simba}

{$f-}

(*
type TRSLobby
~~~~~~~~~~~~~

A type that stores functions and properties of the Runescape lobby.
*)
type
  TRSLobby = record(TRSInterface)
    __placeholder: byte;
  end;

(*
var lobby
~~~~~~~~~

Variable that stores functions and properties of the Runescape lobby.

Example:

.. code-block:: pascal

    if (lobby.isOpen()) then
      writeln('Lobby is open!');
*)
var
  lobby: TRSLobby;

(*
const Lobby Tabs
~~~~~~~~~~~~~~~~

Integer constants that representing the different lobby tabs.

Example:

.. code-block:: pascal

  if (lobby.getTab() = LOBBY_CLAN) then
    writeln('The clan chat tab is open!');

*)
const
  LOBBY_COUNT = 6;
    LOBBY_PLAYER = 0;
    LOBBY_WORLDS = 1;
    LOBBY_FRIENDS = 2;
    LOBBY_CHAT = 3;
    LOBBY_CLAN = 4;
    LOBBY_OPTIONS = 5;

{*
const Internal
~~~~~~~~~~~~~~

Internal lobby constants.
*}
const
  // used in getTabBounds calculations
  __LOBBY_TAB_OFFSET: TPoint = [70, 16];


{*
TRSLobby.__init()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__init();

Initiates the lobby default values. Called at the end of this file.

.. note::

    - by Coh3n
    - Last updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__init();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSLobby.__init();
begin
  with self do
  begin
    name := 'RS Lobby';
    ID := ID_INTERFACE_LOBBY;
    parentID := -1;
    static := true;
    setBounds([21, 110, 782, 587]);
  end;
end;
{$ENDIF}

{*
TRSLobby.__skipEmailScreen()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__skipEmailScreen();

Handles the validate email screen that comes up when you enter to lobby.

.. note::

    - by NCDS & Coh3n
    - Last updated: 8th August 2014 by The Mayor

Example:

.. code-block:: pascal

    lobby.__skipEmailScreen();
*}
{$IFNDEF CODEINSIGHT}
procedure TRSLobby.__skipEmailScreen();
var
  tpa: TPointArray;
  wid, hei: integer;
  b: TBox;
begin
  getClientDimensions(wid, hei);

  findColors(tpa, 658491, 0, 0, wid-1, hei-1);

  if (length(tpa) < 20) then  // the red background of the text at the top
    exit();

  b := getTPABounds(tpa);

  b.offset([0, 370]); //offset the red background bounds to where we think the button should be
  getClientDimensions(wid, hei);
  b.setLimit([0, 0, wid-1, hei-1]); // prevent out of bound errors

  if (not findColorsTolerance(tpa, 14601123, b, 13)) then // color of the "Skip" text
    exit();

  if (inRange(length(tpa), 35, 65)) then
  begin
    mouseBox(tpa.getBounds(), MOUSE_LEFT);
    print('TRSLobby.__skipEmailScreen(): Closed validate email screen', TDebug.SUB);
  end;
end;
{$ENDIF}

(*
TRSLobby.isOpen()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.isOpen(): boolean;

Returns true if we are at the lobby screen is loaded. Uses text recognition of
the tabs.

.. note::

    - by Coh3n
    - Last Updated: 11 September 2013 by Olly

Example:

.. code-block:: pascal

    while (not lobby.isOpen()) do
      wait(100 + Random(400));
*)
function TRSLobby.isOpen(): boolean;
const
  COLOR = 16379862; // top left corner of lobby sword symbol
var
  t: integer;
begin
  t := (getSystemTime() + randomRange(7500, 8500));
  result := false;

  self.__skipEmailScreen();

  result := (getColor(lobby.x1 + 41, lobby.y1 - 105) = COLOR);
end;

(*
TRSLobby.findPlayButton()
~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function findPlayButton(var res: TPoint): boolean;

Returns true if the play button is found somewhere on the client.  Used as an easier
way to detect the lobby screen since some lobby tabs are different sizes, causing
lobby interface to change.

.. note::

    - by Coh3n
    - Last updated: 30 October by Olly

Example:

.. code-block:: pascal

    lobby.findPlayButton(p);
*)
function TRSLobby.findPlayButton(var res: TPoint): boolean;
var
  tpa: TPointArray;
  atpa: T2DPointArray;
  width, height, i: integer;
begin
  result := false;
  getClientDimensions(width, height);

  if (findColorsTolerance(tpa, 3850483, 0, 0, width -1, height -1, 10)) then
  begin
    atpa := tpa.cluster(30, 10);

    for i := 0 to high(atpa) do
      if (inRange(length(atpa[i]), 280, 375)) then
      begin
        res := atpa[i].getMiddle();
        result := true;
        break;
      end;
  end;

  print('TRSLobby.findPlayButton(): result = ' + boolToStr(result), TDebug.SUB);
end;

(*
TRSLobby._getTabBounds()
~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._getTabBounds: TBox;

Returns the bounds of the desired tab as a TBox. You should use lobby.openTab() to open a tab

.. note::

    - by The Mayor
    - Last Updated: 8th August 2014

Example:

.. code-block:: pascal

  lobby._getTabBounds(LOBBY_FRIENDS);

*)
function TRSLobby._getTabBounds(tab: integer): TBox;
begin
  if (not self.isOpen()) then
    exit;

  result := gridBox(tab, 6, 1, 45, 22, 100, 0, point(self.x1 + __LOBBY_TAB_OFFSET.x, self.y1 + __LOBBY_TAB_OFFSET.y));
end;

(*
TRSLobby.getCurrentTab()
~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.getCurrentTab: integer;

Gets current lobby tab.

.. note::

    - by Bionicle1800, NCDS, & Coh3n
    - Last Updated: 8 August 2014 by The Mayor

Example:

.. code-block:: pascal

    if (lobby.getCurrentTab() = LOBBY_WORLDS) then
      writeln('The world list is open!');

*)
function TRSLobby.getCurrentTab(): integer;
const
  INACTIVE_TAB_BOTTOM = 9078134; // Bottom line of inactive tab
begin
  if (not self.isOpen()) then
    exit(-1);

  for result := 0 to (LOBBY_COUNT - 1) do
  begin
    if not (getColor(self._getTabBounds(result).x2, self._getTabBounds(result).y2) = INACTIVE_TAB_BOTTOM) then
    begin
      print('TRSLobby.getCurrentTab(): Current tab: '+toStr(result), TDebug.SUB);
      exit;
    end;
  end;

  Result := -1;
end;

{*
TRSLobby._open()
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._open(): boolean;

Exits from the game to the lobby screen. Should call players[currentPlayer].exitToLobby()
instead of this function.

.. note::

    - by Starblaster100, Raymond, IceFire908, Tarajunky, & Coh3n
    - Last Updated: 23 July 2013 by Co3n

Example:

.. code-block:: pascal

    if (players[currentPlayer].exitToLobby()) then
      writeln('We''ve exited to the lobby!');
*}
function TRSLobby._open(): boolean;
var
  t: integer;
  p: TPoint;
  b: TBox;
begin
  if (lobby.isOpen()) then
    exit(true);

  if (options.open()) then
  begin
    options.selectOption(['Lobby','xit to', 'obby']);

    t := (getSystemTime() + (7000));

    while (not result) and (getSystemTime() < t) do
    begin
      print('Waiting for lobby screen...', TDebug.SUB);
      result := lobby.isOpen();
      wait(700 + random(300));
    end;
  end;
end;

{*
TRSLobby.openTab
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.openTab(tab: integer): boolean;

Opens the lobby tab specified by 'tab'. Valied arguments for 'tab' are the
lobby tab constants at the top of the file. Is an internal SRL function, should
use the lobby file's variables instead.

.. note::

    - by Bionicle1800 & Coh3n
    - Last Updated: 8th August 2014 by The Mayor

Example:

.. code-block:: pascal

    lobby.openTab(LOBBY_WORLDS);

*}
function TRSLobby.openTab(tab: integer): boolean;
var
  p: TPoint;
  currTab: integer;
begin
  if (not self.isOpen()) then
    exit(false);

  currTab := self.getCurrentTab();

  if (currTab = tab) then
    exit(true);

  mouseBox(self._getTabBounds(tab), MOUSE_LEFT);

  wait(200 + random(200));
  result := true;

  if (not result) then
    print('TRSLobby.openTab(): Failed to open tab '+toStr(tab), TDebug.ERROR);
end;

{*
TRSLobby._leave()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._leave(): boolean;

Leaves the lobby screen. This instance of the function is internal.  Use
players[currentPlayer].logout();.

.. note::

    - by IceFire908
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobby.isOpen()) then
      if (not players[currentPlayer].leave()) then
        writeln('Oh no, we didn''t make it out of the Lobby Screen.');
*}
function TRSLobby._leave(): boolean;
var
  t: integer;
  exitButton: TPoint;
begin
  result := (not self.isOpen());

  t := (getSystemTime() + 30000);
  exitButton := point(self.x2 - 9, self.y1 - 77).rand(3);

  while (not result) and (getSystemTime() < t) do
  begin
    mouse(exitButton, MOUSE_LEFT);
    print('Waiting for login screen...', TDebug.SUB);
    wait(randomRange(500, 1000));
    result := (not lobby.isOpen());
  end;
end;

(*
TRSLobby.quickSelectWorld
~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.quickSelectWorld(wo: integer): boolean;

Returns true if the desired world appears in the quick select favorites boxes
in the bottom right of the interface.  Will click the world if found.

.. note::

    - by Coh3n
    - Last Updated: 11 September 2013 by Olly

Example:

.. code-block:: pascal

    if (not lobby.isOpen()) then
      lobby.open();
*)
function TRSLobby.quickSelectWorld(wo: integer): boolean;
var
  woBoxes: TBoxArray;
  i, bmp, foundWo: integer;
  tpa: TPointArray;
  atpa: T2DPointArray;
begin
  result := false;

  if (not self.isOpen()) then
    exit();

  // because the quick-select buttons don't appear in the options tab
  // and buttons are offset on the worlds tab
  if (self.getCurrentTab() = LOBBY_WORLDS) or (self.getCurrentTab() = LOBBY_OPTIONS) then
    if (not self.openTab(LOBBY_PLAYER)) then
      exit();

  // a box around each of the quick select world boxes
  woBoxes := grid(3, 1, 32, 16, 65, 0, point(self.x2 - 195, self.y2 - 54));

  for i := 0 to high(woBoxes) do
  begin
    foundWo := strToIntDef(trim(tesseractGetText(woBoxes[i].x1, woBoxes[i].y1, woBoxes[i].x2, woBoxes[i].y2, FILTER_NPC_CHAT_CHARS, 'digits')), -1);
    print('TRSLobby.quickSelectWorld(): Tesseract found world '+intToStr(foundWo));

    if (foundWo = wo) then
    begin
      mouseBox(woBoxes[i], MOUSE_LEFT);
      print('TRSLobby.quickSelectWorld(): Clicked quick-select world '+toStr(wo), TDebug.SUB);
      exit(true);
    end;
  end;
end;

begin
  lobby.__init();
end;
