(*
Text
====

The text file holds functions and procedures that are used in Runescape text
recognition.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/core/text.simba>`_.

*)

{$include_once ../interfaces/interfaces.simba}
{$include_once debug.simba}
{$include_once mouse.simba}

{$f-}

(*
findText
~~~~~~~~

.. code-block:: pascal

    function findText(var p: TPoint; txt, fonts: TStringArray; searchBox: TBox): boolean;

Uses a bitmap mask to find text on the screen.  Returns true if the text is found.

    * p: The point the text is found.
    * txt: The text to look for.
    * fonts: The font(s) of the text.
    * searchBox: The box to look for the text.

.. note::

    - by Stupid3ooo, modified by LordJashin & Coh3n
    - Last Updated: Feb. 20, 2013 by Coh3n

Example:

.. code-block:: pascal

    var
      p: TPoint;
    begin
      if (findText(p, ['Lobby'], [upCharsEx], tabLogout.bounds)) then
        writeln('Found "Exit to Lobby" text');
      .
      .
      .
    end;
*)
function findText(var p: TPoint; txt, fonts: TStringArray; searchBox: TBox): boolean;
var
  s, i, j, w, h: integer;
begin
  p := [-1, -1];

  for i := 0 to high(txt) do
    for j := 0 to high(fonts) do
    begin
      s := bitmapFromText(toStr(txt[i]), toStr(fonts[j]));

      // to prevent looking outside the client
      if (searchBox.x1 < 0) then searchBox.x1 := 0;
      if (searchBox.y1 < 0) then searchBox.y1 := 0;

      if (findBitmapMaskTolerance(s, p, searchBox, 30, 30)) then
      begin
        getBitmapSize(s, w, h);
        p := point(p.x + round(w / 2), p.y + round(h / 2));
        result := true;
      end;

      freeBitmap(s);

      if (result) then
        break(2);
    end;
end;

(*
findText; overload;
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function findText(var p: TPoint; col, tol: integer; txt, fonts: TStringArray; lookIn: TBox; waitTime: integer = 0): boolean; overload;

Uses TPointArrays to find text on the screen.  Searches for the color (col) with
tolerance (tol) on the screen, then looks for the text (txt) in a TPA created
from the fonts (fonts).  Looks in the box (lookIn) and returns the point found
(p). Returns true if the text is found. If the optional parameter (waitTime) is
set, will wait specified amount of time for the text to be found before returning
false.


.. note::

    - by Raymond and Coh3n
    - Last Updated: Feb. 20, 2013 by Coh3n

Example:

.. code-block:: pascal

    var
      p: TPoint;
    begin
      if (findText(p, clWhite, 10, ['Lobby'], [upCharsEx], gameTabArea)) then
        writeln('Found "Exit to Lobby" text');
      .
      .
      .
    end;
*)
function findText(var p: TPoint; col, tol: integer; txt, fonts: TStringArray; lookIn: TBox; waitTime: integer = 0): boolean; overload;
var
  b: TBox;
  i, j, w, h, t: integer;
  tpaColors, tpaText, matches: TPointArray;
begin
  t := (getSystemTime() + waitTime);
  p := point(-1, -1);

  if (not findColorsTolerance(tpaColors, col, lookIn, tol)) then
    exit(false);

  repeat
    // loop through the fonts and texts to look for
    for i := 0 to high(txt) do
      for j := 0 to high(fonts) do
      begin
        tpaText := TPAFromText(txt[i], fonts[i], w, h);

        if (tpaColors.findTextIn(h - 1, tpaText, matches)) then
        begin
          // sort the tpa matches and return the top left of the text
          b := tpaText.getBounds();
          matches.sortFromPoint(point(b.x1, b.y1));
          p := matches[0];

          result := true;
          break(3);
        end;
      end;
  until(getSystemTime() > t);
end;

function extractTextFromTPA(tpa: TPointArray; minvspacing, maxvspacing, hspacing: integer; font: string): string;
var
  atpa: T2DPointArray;
  b: TBox;
begin
  if (length(tpa) < 1) then
    exit;

  atpa := clusterTPAEx(tpa, minvspacing, hspacing);
  b := getTPABounds(tpa);

  sortATPAFromFirstPointX(atpa, point(b.x1, b.y1 + b.getHeight() div 2));
  result := getTextATPA(atpa, maxvspacing, font);
end;

(*
mouseText
~~~~~~~~~

.. code-block:: pascal

    function mouseText(txt, fonts: TStringArray; searchBox: TBox; mouseAction: integer): boolean;

Returns true if the mouse action is performed on the text.

    * txt: The text to look for.
    * fonts: The font(s) of the text.
    * searchBox: The box to look for the text.
    * mouseAction: The action of the mouse.

.. note::

    - by Coh3n
    - Last Updated: Mar. 1, 2013 by Coh3n

Example:

.. code-block:: pascal

    mouseText(['Lobby'], [upCharsEx], tabLogout.bounds, MOUSE_LEFT);
*)
function mouseText(txt, fonts: TStringArray; searchBox: TBox; mouseAction: integer): boolean;
var
  p: TPoint;
begin
  if (findText(p, txt, fonts, searchBox)) then
  begin
    mouse(p.rand(3, 10), mouseAction);
    exit(true);
  end;
end;

(*
typeByte
~~~~~~~~

.. code-block:: pascal

    procedure typeByte(k: byte);

Types one character k.

.. note::

    - by Mutant Squirrle

Example:

.. code-block:: pascal

    typeByte(VK_ENTER);

*)
procedure typeByte(k: byte);
begin
  {$IFDEF SMART}
  if (k = 13) then
    k := 10;
  {$ENDIF}

  KeyDown(k);
  Wait(10 + Random(50));
  KeyUp(k);
end;

(*
typeByteWait
~~~~~~~~~~~~

.. code-block:: pascal

    procedure typeByteWait(key: Byte; waitTime: integer);

Holds down the desired key for the set waitTime.

.. note::

    - by Ollybest

Example:

.. code-block:: pascal

     typeByteWait(vk_up, 800 + random(200));
*)
procedure typeByteWait(key: Byte; waitTime: integer);
begin
  if (not (isKeyDown(key))) then
    keyDown(key);

  wait(waitTime);

  if isKeyDown(key) then
    keyUp(key);
end;

(*
typeSend
~~~~~~~~

.. code-block:: pascal

    procedure typeSend(text: string; pressEnter: boolean = true);

Types text in a human-like way.  Optional parameter 'pressEnter'.  Will press
enter by default.

.. note::

    - by SKy Scripter
    - Last Updated: January 29th, 2013 by Coh3n

Example:

.. code-block:: pascal

    // to type and press enter
    typeSend('Hello');

    // to type and not press enter
    typeSend('Hello', false);

*)
procedure typeSend(text: string; pressEnter: boolean = true);
var
  i: Integer;
begin
  for i := 1 to Length(Text) do
  begin
    SendKeys(Text[i], 30 + Random(30), 30 + Random(30));
    Wait(40 + Random(40));
  end;

  if (pressEnter) then
    TypeByte(VK_ENTER);
end;

var
  tmpLoadedFont: boolean;

procedure tmp_createFont();
var
  i, j, tmp, bmp: integer;
  tpa: TPointArray;
  atpa: T2DPointArray;
  b: TBox;
begin
  writeln('making this font!');

  forceDirectories(fontPath + 'tmpMouseoverChars/');

  bmp := bitmapFromString(1450, 35, 'meJzt2tty4zoMRFH9/0/rPEydFKML2A0Clu3s9ZTYIASRIKVMzbYBAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAADgM+z7bgX/417i35DpWD3zITLOrJeduLXyyO5irJr7pi5dzLNprTkZR9VmFoP3K+tp3WCrN3KNtGnHiz4biSV2bzMx27U59SHpLnq25uknyldKGXGYfqFEC4kZrNkY87u9IRTelTkRmWukuw8Xg6e/jp8oU6fUMMbrixKk0i+6ktkasr7cYvJpZFNm90JuZjeyfDbcGdCH9JWxD3/1nH+eBk+T6/NsLbcen047Dd7W/hhsCo6/Gm9NnDq9jMQiTmP0sFzwYUL0sS+zckfuJeKx7sqKzWMVnL612szjDdZeIrF8rRKbdDrEOmB/JjmxEfRIpeaOq1sD06f3402VO5cK07pddIgUO8SKSeQMhlgFdx+54mlw/soKtkoSv1W2lXXdQ3zQsZfBevK4N5Sw7syH5o9ruLyEFSPulMtPLr9SpkXs/Mtga06qdkpid7vxKzu3pIy7FVxPay23fqGg65RKlEgxrZKzW18/n+c5OKCs5U4Hu6eoVUas7yg4n7dx8Pps3A0M1jfIrMSLBVvZrMh05kPnv5W+vX8eFQyPT4bLhGJ837S7e98Ndtumdl+8gL6XDwdF3Ehb6ja7l7Lq6m6wPjD9EDn/+jLWHtmvlF9CTHguSaxcv7q1gtOa9eF9zbxec0kZ1tjDNllJdQ6+3IaXSS6D48zKh+c8ia6ryhx8uzJKqS34SpznwwLFE6VsBCv4sqS7ADGVe2k3+RY2/CNqp+41ZRQeSn1nXZ+XPa3iyVk860qOo0Rw4cFyDhaHpPvZSj49nKdfKZmnQ9IPrMXIXPyba72d/cZ6GXrnBEfQotbOsfa+MsNvyD0l9IGJGeg4MfRjtukhYg20dkpiUTq8ptsTTzQl5vA8VQqwFsVdQTc4eAFYzNwUHAxf6SJxnpUuWnxGTJ+w4ga3Vrak6/oyx6wNKw7MlX1Yu2Ci3OOodou58/yCDf7sC8/+W0nktvAPQW4ZerBSw+XPQXDTbOhDEjXnCogvtHjWlR9HuTJi+sFlJbdqHsvQW/qwC+LZ1htPL8PNrIRZkbn4N/eC2/m5REnP3CWPY/TTvvbS6XhrX+TqeVx6L5c/K90jq/ZNwGrOdDMndso00s3fpHZfpzO7YVaHiGFjpJhTcRl8l6G8LdPBeouuNE/J2iXKOGzVeOeOMdNrBSs7/TDXddPM+tkYx4j1WLUFZejb8GeB4vzu5krET2PEbIn4XPDjj57xVzG4/HxuymzF61Ox+VMn1uAOaT2f734951k8RfXD3Mpcvij6qFzXiQP1g/EQGQ/cB0Lhxl95Vs701JXHv7kX3I54vFirpiQ8p01cYvHqK/Guz+pM94iIP3EDcpXoQ6wn+2LmqsPT2im5MlqV7273OeL282HClczxVfbfxDKsmpUMeg3bWs3xqP2KmNmi7BR9NhLX3YeXqLsLnVsoMRvTNpjeppvZmj2lALEepTZliNLS4w3Gi7jNet6tMxHs7pSmag9pV/bvirEMt/mVc0MPbsqsBJwzK4u+coNiMXpY6/l8/vWcp7U3rBZN93NMPxLd5FbNVthd/uBwPvygZHZ3yjRYjHSD3UreX/e9rJyHJZmtU8WS6JyqSz+Sv9bKdu54XruUh8hBVWYrPh0jPiiVG2w9kN/E4sM0fqQm5llZPrHgu+Cg4MXMenA8b/q1VrpOnOeqfXqZefo0PPRGfK27DMHn4innZo4DrKukI63m//lWn5Dz8k27uvYGx0oKE+ZqcJM/Tu//w7dubzySWcmWy2zV3Nd1+jy4ZQQ3eM7jruBm/u1sLXeun0uqXU+u36OVfFrYGGZNtRiW3lklkbn4P+u8ZPpai/nH4KAnE8n1Avriv9vK4VO1/a0zMHeJxFWmTxy3pdP7ru8erTI+QuESx8HBWKsxFl9FguGtU6F3qdXPTSsyflv73nIIvvv5LiAuZnGeA5fH110x7rGfKCn9QqJsLv3gFe/05yt9NsofhYnzuWPhPpH74H6HzOls7lknnqKFBYxh6Xek97dS84P3W/hK+baa7rHvvP36Fak1vvu5Q6y00xeGrbrZ3PcxOmdU8p4fxJc/y1YaSXlV1jMn4sUixZfweCAUub+M4oErjaRnniZ3D0Yx8px8/5+YOa55pYY4+HLgNLNShv653kWXAdY8x9KZxXnuaD+rErcMcULOYWLywjArchxSXsYncs/nd8i8ta14bndbNVhhYs0f16IrBT97s58425amu+s7zL9sObpvZzxbxDD3CBJf5Dq2Uus7wxdLrIW4gvtvhcHbQiNZlYgJrXvU68zNnpj/iyUmQZxk/VpuP+tD9pP1nPG96PmD2dCv5ZaRvkFr6qbB+ufWbFwW45ZhZVZq1lvUqiRRsx6fiLwbaM3G3aiggL7l1kflkn8KfWu/VXJ3Q+U6Mx6SO2TcsOmQjoXDH9R0CPwkL4/MxUPU2g9N+trsuzEbAAB8ltxLmv6PMKmigCN6CX9Wx782s6FaMb0AAAAAcvhrAoixRwAAAADgc/GfiAAF2wQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAl/gNQZaOS');

  findColorsBitmap(bmp, tpa, clWhite);
  atpa := tpa.cluster(1, 10);
  sortaTPAFromFirstPointX(atpa, point(0, 0));

  j := 33;

  for i := 0 to high(atpa) do
  begin
    b := atpa[i].getBounds();

    tmp := createBitmap(b.getWidth()+2, b.getHeight()+2);
    fastDrawClear(tmp, clBlack);

    offsetTPA(atpa[i], point(-b.x1 +1, -b.y1+1));

    drawTPABitmap(tmp, atpa[i], clWhite);
    saveBitmap(tmp, fontPath + 'tmpMouseoverChars/' + toStr(j)+'.bmp');
    freeBitmap(tmp);

    inc(j);
  end;

  tmp := createBitmap(3, 14);
  fastDrawClear(tmp, clBlack);
  saveBitmap(tmp, fontPath + 'tmpMouseoverChars/' + '32.bmp');

  freeBitmap(tmp);
  freeBitmap(bmp);
end;

procedure ff();
begin
  freeFont('tmpMouseoverChars');
end;

function tmp_ColorToGrayL(Color: Integer): Byte;
begin
  Result := Trunc((0.2126 * (Color and $FF)) +
                  (0.7152 * ((Color shr 8) and $FF)) +
                  (0.0722 * ((Color shr 16) and $FF)));
end;

type
  tmp_TThreshMethod = (tmp_TM_Mean, tmp_TM_MinMax);


{
 temp in here until new simba version is released

 Copyright (c) 2013, Jarl K. Holta || https://github.com/WarPie
 All rights reserved.
}
procedure TMufasaBitmap.tmp_Threshold(Alpha, Beta: Integer; Method: tmp_TThreshMethod; C:Integer);
var
  x, y, i, Color, IMin, IMax, ww, hh, w, h: Integer;
  CurThreshold, Counter: Integer;
  Temp: array of TByteArray;
  Tab: array[0..256] of Byte;
begin
  w := self.getWidth();
  h := self.getHeight();

  SetLength(Temp, (H + 2), (W + 2));

  ww := w;;
  hh := h;
  Dec(ww);
  Dec(hh);

  CurThreshold := 0;

  case Method of
    tmp_TM_Mean:
    begin
      for y := 0 to hh do
      begin
        Counter := 0;
        for x := 0 to ww do
        begin
          Color := tmp_ColorToGrayL(Self.GetPixel(x, y));
          Temp[(y + 1)][(x + 1)] := Color;
          Counter := (Counter + Color);
        end;
        CurThreshold := (CurThreshold + (Counter div W));
      end;
      if (C < 0) then
        CurThreshold := ((CurThreshold div H) - Abs(C))
      else
        CurThreshold := ((CurThreshold div H) + C);
    end;

    tmp_TM_MinMax:
    begin
      IMin := tmp_ColorToGrayL(Self.GetPixel(0, 0));
      IMax := IMin;
      for y := 0 to hh do
        for x := 0 to ww do
        begin
          Color := tmp_ColorToGrayL(Self.GetPixel(x, y));
          Temp[(y + 1)][(x + 1)] := Color;
          if (Color < IMin) then
            IMin := Color
          else
            if (Color > IMax) then
              IMax := Color;
        end;
      if (C < 0) then
        CurThreshold := (((IMax + IMin) div 2) - Abs(C))
      else
        CurThreshold := (((IMax + IMin) div 2) + C);
    end;
  end;

  CurThreshold := Max(0, Min(CurThreshold, 255));
  for i := 0 to (CurThreshold - 1) do
    Tab[i] := Alpha;
  for i := CurThreshold to 255 do
    Tab[i] := Beta;

  for y := 1 to h do
    for x := 1 to w do
      Self.SetPixel((x - 1), (y - 1), Tab[Temp[y][x]]);
end;

{*
__findMouseOverBox
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function __findMouseOverBox(out box: TBox): boolean;

Returns true if the mouse-over box is found on the client.  Returns it's coordinates
in 'box'.

.. note::

    - by euphemism
    - Last Modified: 12 August 2013 by euphemism

Example:

.. code-block:: pascal

    if (__findMouseOverBox(tmpBox)) then
      writeln('Found mouse-over box!');
*}
{$IFNDEF CODEINSIGHT}
function __findMouseOverBox(out box: TBox): boolean;
const
  BORDER_COLOR = 4208424;
  BORDER_TOLERANCE = 7;
  CORNER_COLOR = 3616289;
var
  p: TPoint;
  clientArea, searchBox: TBox;
  tpaBorder, tpaBorderPoints, tpa: TPointArray;
  atpa: T2DPointArray;
  i, h: integer;
  b: TBox;
begin
  result := false;

  getMousePos(p.x, p.y);
  getClientDimensions(clientArea.x2, clientArea.y2);

  dec(clientArea.x2); dec(clientArea.y2);

  searchBox := [p.x - 200, p.y - 100, p.x + 350, p.y + 150];
  searchBox.setLimit(clientArea);

  if (findColorsTolerance(tpaBorder, BORDER_COLOR, searchBox, BORDER_TOLERANCE)) then
    if (findColors(tpaBorderPoints, CORNER_COLOR, searchBox)) then
    begin
      tpa := combineTPA(tpaBorder, tpaBorderPoints);
      atpa := clusterTPAEx(tpa, 1, 1);
      sortATPASize(atpa, true);

      h := high(atpa);

      for i := 0 to h do
      begin
        if (length(atpa[i]) < 15) then
          continue;

        b := atpa[i].getBounds();

        if (b.getWidth() < 50) or (b.getHeight() < 15) then // basic size check
          continue;

        result := (getColor(b.x1+1, b.y1) = CORNER_COLOR);// check corner is a corner color

        if (result) then
        begin
          box := [b.x1 +1, b.y1 +1, b.x2 -1, b.y2 -1];  // set the result (decrease bounds by 1)
          break;
        end;
      end;
    end;
end;
{$ENDIF}

{*
__getMouseOverText
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function __getMouseOverText(box: TBox; cleanBitmap, filteredBitmap: integer): string;

Returns the mouse-over text found inside the 'box' based on the clean and
filtered bitmaps.

.. note::

    - by euphemism
    - Last Modified: 12 August 2013 by euphemism

Example:

.. code-block:: pascal

    if (__findMouseOverBox(tmpBox)) then
    begin
      __createMouseOverFilter(tmpBox, bmpClean, bmpFiltered);
      text := __getMouseOverText(tmpBox, bmpClean, bmpFiltered);
      writeln('Mouse-over text: '+text);
    end;
*}
{$IFNDEF CODEINSIGHT}
function __getMouseOverText(box: TBox): string;
var
  tpa: TPointArray;
  atpa: T2DPointArray;
  bmp, wid, hei, i, h: integer;
begin
  if (not directoryExists(fontPath + 'tmpMouseoverChars')) then
    tmp_createFont();

  if (not tmpLoadedFont) then
  begin
    loadFont('tmpMouseoverChars', false);
    addOnTerminate('ff');
    tmpLoadedFont := true;
  end;

  result := '';

  bmp := bitmapFromClient(box);
  getBitmapSize(bmp, wid, hei);

  getMufasaBitmap(bmp).tmp_Threshold(0, 255, tmp_TM_Mean, 100);

  if (findColorsBitmap(bmp, tpa, clRed)) then
  begin
    atpa := tpa.cluster(wid, 2);  // reads : as .. but it will do for now. (split rows)
    sortATPAFromFirstPointY(atpa, point(wid div 2, 0));

    h := high(atpa);

    for i := 0 to h do
      result := result + extractTextFromTPA(atpa[i], 1, 4, 5, {MouseoverCharsRS3}'tmpMouseoverChars') + ' ';
   end;

  freeBitmap(bmp);
end;
{$ENDIF}

(*
getMouseOverText
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function getMouseOverText(): string;

Returns the current mouse-over text.

.. note::

    - by euphemism & Coh3n
    - Last Modified: 12 August 2013 by Coh3n

Example:

.. code-block:: pascal

    writeln(getMouseOverText());
*)
function getMouseOverText(): string;
var
  box: TBox;
begin
  result := '';

  if (__findMouseOverBox(box)) then
    result := __getMouseOverText(box);
end;

(*
isMouseOverText
~~~~~~~~~~~~~~~

.. code-block:: pascal

    function isMouseOverText(txt: TStringArray; waitTime: integer = 100): boolean;

Returns true if one of the indexes in 'txt' is found in the current mouse-over text.
Will wait up to 'waitTime' to find the text before exiting the function.

.. note::

    - by Coh3n
    - Last Modified: 12 August 2013 by Coh3n

Example:

.. code-block:: pascal

    if (isMouseOverText(['Tree'])) then
    begin
      getMousePos(x, y);
      mouse(point(x, y), MOUSE_LEFT);
    end;
*)
function isMouseOverText(txt: TStringArray; waitTime: integer = 150): boolean;
var
  i, t: integer;
  s: string;
begin
  print('isMouseOverText()', TDebug.HEADER);
  t := (getSystemTime() + waitTime);

  repeat
    s := getMouseOverText();
    print('Current mouse-over text: "'+s+'"');

    for i := 0 to high(txt) do
      if (pos(txt[i], s) > 0) then
      begin
        print('Found "'+txt[i]+'" in mouse-over string: "'+s+'"');
        result := true;
        break(2);
      end;

    wait(20 + random(20));
  until (getSystemTime() > t);

  print('isMouseOverText(): '+toStr(result), TDebug.FOOTER);
end;

(*
isMouseOverMenuOpen
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function isMouseOverMenuOpen(var res: TBox; waitTime: integer = 100): boolean;

Returns true if the mouse-over menu is open.  Will wait a maximum of
'waitTime' for the menu to appear.

.. note::

    - by Coh3n
    - Last Modified: 12 August 2013 by Coh3n

Example:

.. code-block:: pascal

    if (isMouseOverMenuOpen()) then
      writeln('Mouse-over menu is open');
*)
function isMouseOverMenuOpen(var res: TBox; waitTime: integer = 100): boolean;
var
  t: integer;
begin
  res := [-1, -1, -1, -1];
  t := (getSystemTime() + waitTime);

  while ((getSystemTime() < t) and (not result)) do
  begin
    result := __findMouseOverBox(res);
    wait(20 + random(20));
  end;

end;







