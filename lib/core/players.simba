(*
Player Management
=================

The Players file stores the functions related to the Players array. It also
stores properties of the TPlayer type.

*)

{$include_once ../interfaces/lobby.simba}
{$include_once ../interfaces/mainscreen.simba}
{$include_once ../core/mouse.simba}

(*
Constant: Skills
~~~~~~~~~~~~~~~~

Integer constants that represent each skill.

Example:

.. code-block:: pascal

    players[currentPlayer].skillLevel[SKILL_ATTACK] := 50;

*)
const
  SKILL_COUNT = 25;
    SKILL_ATTACK = 0;
    SKILL_DEFENCE = 1;
    SKILL_STRENGTH = 2;
    SKILL_HITPOINTS = 3;
    SKILL_RANGE = 4;
    SKILL_PRAYER = 5;
    SKILL_MAGIC = 6;
    SKILL_COOKING = 7;
    SKILL_WOODCUTTING = 8;
    SKILL_FLETCHING = 9;
    SKILL_FISHING = 10;
    SKILL_FIREMAKING = 11;
    SKILL_CRAFTING = 12;
    SKILL_SMITHING = 13;
    SKILL_MINING = 14;
    SKILL_HERBLORE = 15;
    SKILL_AGILITY = 16;
    SKILL_THIEVING = 17;
    SKILL_SLAYER = 18;
    SKILL_FARMING = 19;
    SKILL_RUNECRAFTING = 20;
    SKILL_HUNTER = 21;
    SKILL_CONSTRUCTION = 22;
    SKILL_SUMMONING = 23;
    SKILL_DUNGEONEERING = 24;

const
  LOGIN_COLOR_TEXT = 12378347;

var
  boxUsername, boxPassword: TBox; // set at the end of this file

(*
Type: TPlayer
~~~~~~~~~~~~~

A record that stores useful information about the current players.  It is up to
the scripter to set these attributes in their scripts.

Example:

.. code-block:: pascal

    with players[0] do
    begin
      loginName := 'example@example.com';
      displayName := 'Sir Example III';
      nickname := 'fighter1';
      password := 'thisismypassword12345';
    end;

*)
type
  TPlayer = record
    loginName: string;
    displayName: string;
    nickname: string;
    password: string;
    bankPin: string;
    location: string;
    isActive: boolean;
    isMember: boolean;
    findMod: boolean;
    findTrade: boolean;
    reincarnate: boolean;
    timeWorked: integer;
    world: integer;
    skillLevel: array[0..(SKILL_COUNT - 1)] of integer;
    booleans: array[0..99] of boolean;
    integers: array[0..99] of integer;
    strings: array[0..99] of string;
    extendeds: array[0..99] of extended;
    variants: array[0..99] of variant;
  end;

  TPlayerArray = array of TPlayer;

(*
Player Variables
~~~~~~~~~~~~~~~~

All the variables associated with players.simba.

  * players: used to store player information.
  * currentPlayer: the player currently running.
  * playerStartTime: used to calcualte players[].worked.

*)
var
  players: TPlayerArray;
  currentPlayer: integer;
  playerStartTime: longInt;

function getPlayers(): TPlayerArray; forward; // from player_info.simba

(*
TPlayerArray.getActive
~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TPlayerArray.getActive(): integer;

Returns the number of players that are still active in the TPlayerArray.

.. note::

    by ss23 & Coh3n
    Last updated: Dec. 22nd, 2012 by Coh3n

Example:

.. code-block:: pascal

    while (players.getActive() > 0) do
      mainloop();
*)
function TPlayerArray.getActive(): integer;
var
  i, h: integer;
begin
  result := 0;
  h := high(self);

  for i := 0 to h do
    if (self[i].isActive) then
      inc(result);
end;

(*
TPlayerArray._add
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TPlayerArray._add(info: TPlayer);

Adds the player with 'info' to the TPlayerArray.

.. note::

    by Zyt3x & Coh3n
    Last updated: Dec. 22nd, 2012 by Coh3n

Example:

.. code-block:: pascal

    var
      P: TPlayer;
      Players: TPlayerArray;
    begin
      with P do
      begin
        loginName := 'example@example.com';
        password := 'examplepassword123';
      end;

      Players._add(P);
    end;
*)
procedure TPlayerArray._add(const info: TPlayer);
var
  h: integer;
begin
  setLength(self, length(self) + 1);
  self[high(self)] := info;

  h := high(self);
  self[h] := info;
  self[h].isActive := true;
  self[h].findMod := true;
end;

(*
TPlayerArray.setup
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TPlayerArray.setup(names: TStringArray);

Sets up the players with login name, display name, or nickname that match
elements of 'names'.  Called only in DeclarePlayers().

.. note::

    by Zyt3x & Coh3n
    Last updated: Dec. 22nd, 2012 by Coh3n

Example:

.. code-block:: pascal

   players.setup(['woodcutter5', 'miner3', 'fisher6']);

*)
procedure TPlayerArray.setup(names: TStringArray);
var
  i, j: integer;
  tmp: TPlayerArray;
begin
  tmp := getPlayers();

  for i := 0 to high(names) do
    for j := 0 to high(tmp) do
    begin
      if (names[i] = tmp[j].loginName) or (names[i] = tmp[j].displayName) or (names[i] = tmp[j].nickname) then
      begin
        self._add(tmp[j]);
        break;
      end;

      if (j = high(tmp)) then
        debug('Invalid player name: '+names[i]);
    end;
end;

(*
TPlayer.write
~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TPlayer.write();

Writes the standard information about the player.

.. note::

    by Coh3n
    Last updated: Dec. 22nd, 2012 by Zyt3x

Example:

.. code-block:: pascal

    players[currentPlayer].write();

*)
procedure TPlayer.write();
begin
  writeln('Login Name: ', loginName);
  writeln('Display Name: ', displayName);
  writeln('Nickname: ', nickname);
  writeln('Member: ', isMember);
  writeln('Active: ', isActive);
end;

function TPlayer.next(active: boolean): boolean;
begin
end;

(*
TPlayer.addWorked
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TPlayer.addWorked();

Increased the player's .worked property so it can be accessed later.

.. note::

    by SRL Community
    Last updated: Nov. 1st, 2012 by Coh3n

Example:

.. code-block:: pascal

    if (bankedLogs = true) then
      players[currentPlayer].addWorked();

*)
procedure TPlayer.addWorked();
begin
  if (playerStartTime > 0) then // if it's been set at all
    incEx(self.timeWorked, (getSystemTime - playerStartTime));

  playerStartTime := getSystemTime();
end;

(*
isLoggedIn()
~~~~~~~~~~~~

.. code-block:: pascal

    function isLoggedIn(): boolean;

Returns true if a player is logged in.

.. note::

    by WT-Fakawi
    Last updated: 30/09/2012 by Coh3n

Example:

.. code-block:: pascal

    if (isLoggedIn()) then
      writeln('We are logged in!');
*)
function isLoggedIn(): boolean;
begin
  Result := (GetColor(478, 543) = 16777215); // white text on report abuse button
end;

{*
__enterLoginInfo()
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function __enterLoginInfo(const inputBox: TBox; const info: string; clickBox: boolean = true): boolean;

Used to enter a players username and password to login to RS. Input box can be
any text box, but the username and password boxes are what it was designed for.
The input text (info) is the username/password.  It will click the text box if
clickBox is set to true.  See TPlayer.loginToLobby for usage.

.. note::

    by Starblaster100, Raymond, IceFire908, Tarajunky & NCDS
    Last updated: 30/09/2012 by Coh3n

Example:

.. code-block:: pascal

    __enterLoginInfo(self.loginName);
*}
function __enterLoginInfo(const inputBox: TBox; const info: string; clickBox: boolean = true): boolean;
var
  tpa: TPointArray;
  p: TPoint;
  c, t: integer;
begin
  t := (getSystemTime + (60000 * 2));

  repeat
    // check to see if there is already text in the input box
    findColors(tpa, LOGIN_COLOR_TEXT, inputBox);

    // click input box; clicks to the right of existing text
    if (length(tpa) > 0) then // if there's text in the box
    begin
      sortTPAFrom(tpa, point(inputBox.x2, (inputBox.y1 + inputBox.y2 div 2))); // the middle right of the input box
      p := point(tpa[0].x + 20, tpa[0].y);

      // make sure the point is in the inputBox
      if (not pointInBox(p, inputBox)) then
        p := point(inputBox.x2 - (10 + random(10)), p.y);

      if (clickBox) then
        mouse(p.rand(3), MOUSE_LEFT);
    end else begin
      if (clickBox) then
        mouseBox(inputBox, MOUSE_LEFT);

      break;
    end;

    // delete any existing text
    c := 0;
    while (countColor(LOGIN_COLOR_TEXT, inputBox) > 0) and (c < 25) do
    begin
      inc(c);
      typeByte(VK_BACK);
      wait(50+random(50));
    end;

  until(not findColors(tpa, LOGIN_COLOR_TEXT, inputBox) or (getSystemTime > t));

  wait(100 + random(200));
  typeSend(info);
  result := true;
end;

function TPlayer.__loginActions(const action: string): boolean;
begin
  case (action) of
    'false':
      self.next(false);

    'true':
      self.next(true);

    'halt':
      halt();

    'update':
      begin
        if (@SRL_Events[EVENT_RS_UPDATE] <> nil) then
        begin
          debug('Waiting 5 minutes before calling update procedure');
          wait(5 * 60000);
          SRL_Events[EVENT_RS_UPDATE];
          exit(false);
        end;

        halt();
      end;
  end;
end;

const
  __RES_DEBUG  = 0;
  __RES_WAIT   = 1;
  __RES_TRIES  = 2;
  __RES_ACTION = 3;
  __RES_LOC    = 4;
function TPlayer.__getLoginResponse(colorCount, timer: integer): TVariantArray;
begin
  case (colorCount) of
    //               debug message      |      wait time | tries | action | player[].location
    763, 2348, 2299:
          result := ['Invalid username/password',  0,         2,  'false',  'Wrong User/Pass'      ];
    1088: result := ['Account has been disabled',  0,         0,  'false',  'Acc Disabled'         ];
     951: result := ['Already logged in',          5000,      5,  'true',   'Already logged in'    ];
    1239: result := ['Not a RS member',            0,         1,  'true',   ''                     ];
     364: result := ['Error connecting',           20000,     9,  'halt',   'Error Connecting'     ];
    1057: result := ['Too many incorrect logins',  5 * 60000, 2,  'false',  'Too many logins'      ];
     732: result := ['Runescape has been updated', 0,         0,  'update', 'RS Updated'           ];
     591: result := ['Client token changed',       0,         0,  'update', 'Client Token Changed' ];
    1606: result := ['Game session expired',       0,         0,  'update', 'Session Expired'      ];
     777: result := ['Login limit exceeded',       20000,     10, '',       'Waiting for login.'   ];
     850: result := ['Skill level 1000+ required', 0,         9,  'true',   '1000 skill club'      ];
    1028: result := ['Player in member-only area', 0,         1,  'false',  'In member-only area'  ];

    else
      if ((getSystemTime() > timer) and (timer > 0)) then
      begin
        result :=   ['One minute as passed...',    0,         5,  'false',  'Login failed'         ];
        debug('Unknown color count: '+toStr(colorCount));
        takeScreenshot(logPath + 'unknown_login_count.png');
      end;
  end;
end;

function TPlayer.__loginResponse(const tries: integer; const timer: integer): boolean;
var
  c: integer;
  arr: TVariantArray;
begin
  result := true;

  // counts the colors in the lobby pop up screen
  c := countColor(LOGIN_COLOR_TEXT, 260, 190, 510, 450);
  arr := self.__getLoginResponse(c, timer);

  if (length(arr) > 0) then // if there was an error logging in
  begin
    debug('Login response: '+arr[__RES_DEBUG]);
    wait(2000 + arr[__RES_WAIT] + randomRange(-200, 200));
    typeByte(VK_ESCAPE); // exit response window

    // TODO: need self.world/self.member set here (see old LoginPlayer)

    if (tries <= arr[__RES_TRIES]) then
    begin
      wait(500 + random(500));
      exit(false);
    end;

    if (arr[__RES_LOC] <> '') then
      self.location := arr[__RES_LOC];

    self.__loginActions(arr[__RES_ACTION]);
  end;
end;

(*
TPlayer.loginToLobby
~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TPlayer.loginToLobby(): boolean;

Logs the TPlayer into to lobby.

Example:

.. code-block:: pascal

    players[currentPlayer].loginToLobby();

*)
function TPlayer.loginToLobby(): boolean;
var
  arr: TVariantArray;
  c, t, tries: integer;
begin
  if (isLoggedIn() or isLobbyScreenOpen()) then
    exit(true);

  // check player activity
  if (not self.isActive) then
  begin
    debug('Player '+self.nickname+' is not active...');
    exit(false);
  end;

  typeByte(VK_ESCAPE); // exit any existing windows
  t := (getSystemTime + 60000);

  repeat
    inc(tries);

    debug(capitalize(self.loginName)+' ('+capitalize(self.displayName)+')');
    __enterLoginInfo(boxUsername, self.loginName);
    __enterLoginInfo(boxPassword, self.password, false);

    wait(800 + random(50));	// fixes the login spam recoveries bug

    repeat
      // returns false if exceeded maximum tries (set in __getLoginResponse)
      if (not self.__loginResponse(tries, t)) then
        break;

      // to handle the validate email screen
      if (getColor(473, 468) = 1001495) then // gold border around the "confirm" button
        mouseBox(intToBox(502, 474, 548, 485), MOUSE_LEFT); // the "skip" button

      wait(100 + random(100));
      result := (isLobbyScreenOpen());

    until(result);
  until(result);

  if (result) then
    takeScreenshot('IP_address.png');
end;

function TPlayer.login(): boolean;
var
  tries, t: integer;
begin
  repeat
    if (isLoggedIn()) then
    begin
      result := true;
      break;
    end;

    inc(tries);

    if (self.loginToLobby()) then
    begin
      // TODO: need world switching code here (see old LoginPlayer)


      // the "Play" button
      mouseBox(intToBox(270, 500, 490, 520), MOUSE_LEFT);

      repeat
        // returns false if exceeded maximum tries (set in __getLoginResponse)
        if (not self.__loginResponse(tries, -1)) then
          break(2);

        wait(100 + random(100));
        result := (isLoggedIn());

      until(result);
    end;
  until((tries >= 0) or (result));

  if (result) then
  begin
    debug('Logged in: '+self.nickname);
    playerStartTime := getSystemTime();
    //__exitedSquealOfFortune := false;

    if (@SRL_Events[EVENT_LOGIN] <> nil) then
      SRL_Events[EVENT_LOGIN]();
  end;

  wait(300 + random(500));
end;

{*
__logout()
~~~~~~~~~~

.. code-block:: pascal

    function __logout(): boolean;

Returns true if the current player is successfully logged out.

.. note::

    by Starblaster100, Raymond, IceFire908, Tarajunky & NCDS
    Last updated: 30/09/2012 by Coh3n

Example:

.. code-block:: pascal

    if (players[currentPlayer].logout()) then
      writeln('We are logged out!');
*}
function __logout(): boolean;
var
  x, y, t: integer;
begin
  if (not isLoggedIn()) then
  begin
    result := true;
    exit;
  end;

  if (@SRL_Events[EVENT_LOGOUT] <> nil) then
    SRL_Events[EVENT_LOGOUT]();

  if (not gameTab(TAB_LOGOUT)) then
  begin
    closeWindow(); // incase the bank or another interface is open

    if (not gameTab(TAB_LOGOUT)) then
      exit;
  end;

  wait(100 + random(200));

  if (findText(x, y, ['Login'], [UpCharsEx], MI_BOX, 1000)) then
  begin
    mouse(point(x, y).rand(3, 10), MOUSE_LEFT);
    t := (getSystemTime + (15000));

    while (not result) and (getSystemTime < t) do
    begin
      subDebug('Waiting for login screen...');
      result := (not isLoggedIn());
      wait(700 + random(500));
    end;
  end;
end;

function TPlayer.logout(): boolean;
begin
  result := __logout();
end;

begin
  boxUsername := intToBox(279, 205, 486, 215);
  boxPassword := intToBox(279, 250, 486, 265);
end;
