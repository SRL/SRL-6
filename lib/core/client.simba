(*
Client
======

The Client file holds functions that have anything to do with the RS client or
client settings.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/core/client.simba>`_.

*)

{$include_once ../interfaces/lobby/lobby.simba}
{$include_once ../core/players.simba}

{$f-}

(*
isClientReady
~~~~~~~~~~~~~

.. code-block:: pascal

    function isClientReady: Boolean;

Returns true if we are ready to auto (on loginscreen or logged in).
Useful for waiting until RS has fully loaded.

.. note::

    - by ZephyrsFury
    - Last updated: 22 July 2013 by Coh3n

Example:

.. code-block:: pascal

    while (not isClientReady()) do
      SleepAndMoveMouse(100 + Random(500));
*)
function isClientReady(): boolean;
begin
  Result := (isLoggedIn(true) or lobby.isOpen() or __setInputBoxes());
end;

(*
waitClientReady
~~~~~~~~~~~~~~~

.. code-block:: pascal

    function waitClientReady(): boolean;

Waits until the client is ready.  Will terminate the script if the client loaded
isn't set to fixed size.

.. note::

    - by Coh3n
    - Last updated: 18/04/2012 by Ollybest

Example:

.. code-block:: pascal

    if (waitClientReady()) then
      writeln('Client is ready!');

*)
function waitClientReady(): boolean;
var
  t: UInt32;
  i: integer;
begin
  result := false;

  t := getSystemTime();
  i := 1;

 {$IFNDEF SMART}
   print('You are not using SMART, be sure to use Simba''s crosshairs'+
        ' to select the RS client.', TDebug.HINT);
 {$ENDIF}

  print('Waiting up to 5 minutes for RS to load...');

  while ((getSystemTime() - t) < (5 * 60000)) do // 5 minutes
  begin
    if (isClientReady()) then
      exit(true);

    if ((getSystemTime() - t) > (i * 60000)) then // each minute
    begin
      print(intToStr(i) + ' minute(s) have passed, client is not ready yet');
      inc(i);
    end;

    wait(2000);
  end;
end;

