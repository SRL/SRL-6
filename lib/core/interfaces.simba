(*
Interfaces
==========

The interfaces include contains any functions that are interface-specific such
as bank screens, deposit box screens, or trade screens.

*)

(*
srl_TItemInterface Record
~~~~~~~~~~~~~~~~~~~~~~~~~

Record holding information of an interface of items. The record is structured as
follows:

start: **TPoint**
	The coordinates associated with the Item Interface. Typically, this denotes
	the origin of the colums and rows.

rowsize,colsize: **Integer**
	The dimensions of the rows or columns respectively. This is a measurement in
	pixels.

rownumber,colnumber: **Integer**
	The number of rows or colums respectively present in the ItemInterface
	which creates the grid.

valid: **function: Boolean**
	Functional pointer to check if the respective ItemInterface is available at
	this time. For example:

	.. code-block:: pascal

		bankArea := AreaInfo(SRL_ITEMINT_BANK);
		if (bankArea.valid()) then
		//...

*)

type
  srl_TItemInterface = record
    start: TPoint;      // area coords
    rowsize,colsize: Integer;  //w,h of cols,rows
    colnumber,rownumber: Integer;  //num of cols,rows
    valid: function: Boolean;
  end;

(*

SRL_ITEMINT Consts
~~~~~~~~~~~~~~~~~~

Constants created for selecting precreated `TItemInterfaces\'s`_. Use these
constants for loading SRL's TItemInterfaces.

The constant suffixes[#]_ are as follows:

- *INVENTORY*

- *SHOP*

- *BANK*

- *DEPOSITBOX*

- *TRADE*

- *TRADE_YOUR*

.. note::
	Modified: 13 Jan 2011 - Nava2

.. _TItemInterfaces\'s: `srl_TItemInterface Record`

.. [#] All constants here are prepended with ``SRL_ITEMINT_``.

*)

const
  SRL_ITEMINT_INVENTORY  = 0;
  SRL_ITEMINT_SHOP       = 1;
  SRL_ITEMINT_BANK       = 2;
  SRL_ITEMINT_DEPOSITBOX = 3;
  SRL_ITEMINT_TRADE      = 4;
  SRL_ITEMINT_TRADE_YOUR = 5; // TODO: rename?

(*
BankScreen
~~~~~~~~~~

.. code-block:: pascal

    function BankScreen: Boolean;

Finds Bankscreen. Returns true if Found.

.. note::

    by The SRL Dev Team

Example:

.. code-block:: pascal

*)
function BankScreen: Boolean;
var
  c, x, y: Integer;
begin
  if (CountColor(0, MSX1, MSY1, MSX2, MSY2) > 113000) then
  begin
    MouseBox(26, 138, 489, 157, 1);
    c := GetTimeRunning + 45000;
    while (not BankScreen) and (GetTimeRunning < c) do
      Wait(50);
    Wait(RandomRange(100, 150));
  end;
  Result := FindTextTPA(4106994, 20, 20, 22, 400, 45, x, y, 'Bank', upchars, mouse_none);
end;

(*
DepositScreen
~~~~~~~~~~~~~

.. code-block:: pascal

    function DepositScreen: Boolean;

Returns true if deposit screen is Found.

.. note::

    by Shuttleu

Example:

.. code-block:: pascal

*)
function DepositScreen: Boolean;
var
  X, Y: Integer;
begin
  Result := FindText(x, y, 'Deposit Box', upchars, 92, 29, 396, 54);
end;

(*
PinScreen
~~~~~~~~~

.. code-block:: pascal

    function PinScreen: Boolean;

Finds Pin Screen. Returns true if Found.

.. note::

    by NaumanAkhlaq

Example:

.. code-block:: pascal

*)
function PinScreen : Boolean;
var
  x, y: integer;
begin
  Result := FindTextTPA(2070783, 0, 90, 50, 160, 85, x, y, 'Please', UpChars, mouse_none);
end;

(*
TradeScreen
~~~~~~~~~~~

.. code-block:: pascal

   function TradeScreen: Integer;

Returns ``SRL_ITEMINT_TRADE`` or ``SRL_ITEMINT_TRADE_YOUR`` for the respective
tradescreens, ``-1`` if neither is open.

.. note::

	by Zephyrsfury

.. note::

	Modified: 13 Jan 2011 - Nava2

Example:

.. code-block:: pascal

	if (TradeScreen > 0) then
	  WriteLn('We''re at a trade screen!');

*)

function GetTradeScreen: Integer;
begin
  if (CountColor(16776960, 127, 32, 373, 51) > 20) then
    Result := SRL_ITEMINT_TRADE
  else if (GetColor(93, 69) = 0) then
    Result := SRL_ITEMINT_TRADE_YOUR   //confirmation??
  else
    Result := -1;
end;

(*

TradeScreen
~~~~~~~~~~~

.. code-block:: pascal

	function isTradeScreen: Boolean;

Returns true if one of the trade screens is open.

Example:

.. code-block:: pascal

	if (isTradeScreen) then
	  clickConfirm;

.. note::

	Modified: 13 Jan 2011 - Nava2
*)

function TradeScreen: Boolean;
begin
  Result := (getTradeScreen > 0);
end;

(*
ShopScreen
~~~~~~~~~~

.. code-block:: pascal

    function ShopScreen : Boolean;

Returns true if a shop interface is opened.

.. note::

	by ZephyrsFury fixed by NCDS

Example:

.. code-block:: pascal

.. note::

	Modified: 13 Jan 2011 - Nava2

*)

function ShopScreen : Boolean;
var
  x, y: integer;
begin
  Result := FindTextTPA(
      2070783, 10, 58, 303, 85, 317, x, y, 'Right', StatChars, mouse_none
    );
end;

(*
ShopSwitchTab
~~~~~~~~~~~~~

.. code-block:: pascal

	procedure ShopSwitchTab(name:string);

Switches the shop tab to the one you want, only moves mouse if
neccessary ShopSwitchTab('main') or ShopSwitchTab('player').

.. note::

	by Rasta Magician & ZephyrsFury

Example:

.. code-block:: pascal

	ShopSwitchTab('main');

.. note::
	Modified: Unknown
*)

procedure ShopSwitchTab(Name: string);
var
  x, y: integer;
begin
  if (not(InStrArr(Name, ['main', 'player'], False))) then
  begin
    srl_Warn('ShopSwitchTab', 'Name: ' + Name + ' is not a valid tab.', warn_AllVersions);
    Exit;
  end;
  if (ShopScreen) then
    FindTextTpa(1930687, 0, 50, 65, 225, 83, x, y, Capitalize(Name), SmallChars, mouse_left);
end;
