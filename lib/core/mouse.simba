(*

Mouse
=====

Mouse routines.

*)

const
  //mouse_Right   = 0;
  //mouse_Left    = 1; Defined in the MML
  //mouse_Middle  = 2;
  mouse_Move      = 3;
  mouse_None      = 4;

var
  MouseSpeed: Integer;

  {$IFDEF UseLaptopMouse}
  LMouse_MissChance: Integer;
  {$ENDIF}

(*
WindMouse
~~~~~~~~~

.. code-block:: pascal

    procedure WindMouse(xs, ys, xe, ye, gravity, wind, minWait, maxWait, maxStep, targetArea: extended);


.. note::

    by Benland100

Example:

.. code-block:: pascal

*)
procedure WindMouse(xs, ys, xe, ye, gravity, wind, minWait, maxWait, maxStep, targetArea: extended);
var
  veloX, veloY, windX, windY, veloMag, dist, randomDist, lastDist, step: extended;
  lastX, lastY: integer;
  sqrt2, sqrt3, sqrt5: extended;
begin
  sqrt2:= sqrt(2);
  sqrt3:= sqrt(3);
  sqrt5:= sqrt(5);
  while hypot(xs - xe, ys - ye) > 1 do
  begin
    dist:= hypot(xs - xe, ys - ye);
    wind:= minE(wind, dist);
    if dist >= targetArea then
    begin
      windX:= windX / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
      windY:= windY / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
    end else
    begin
      windX:= windX / sqrt2;
      windY:= windY / sqrt2;
      if (maxStep < 3) then
      begin
        maxStep:= random(3) + 3.0;
      end else
      begin
        maxStep:= maxStep / sqrt5;
      end;
    end;
    veloX:= veloX + windX;
    veloY:= veloY + windY;
    veloX:= veloX + gravity * (xe - xs) / dist;
    veloY:= veloY + gravity * (ye - ys) / dist;
    if hypot(veloX, veloY) > maxStep then
    begin
      randomDist:= maxStep / 2.0 + random(round(maxStep) div 2);
      veloMag:= sqrt(veloX * veloX + veloY * veloY);
      veloX:= (veloX / veloMag) * randomDist;
      veloY:= (veloY / veloMag) * randomDist;
    end;
    lastX:= Round(xs);
    lastY:= Round(ys);
    xs:= xs + veloX;
    ys:= ys + veloY;
    if (lastX <> Round(xs)) or (lastY <> Round(ys)) then
      MoveMouse(Round(xs), Round(ys));
    step:= hypot(xs - lastX, ys - lastY);
    wait(round((maxWait - minWait) * (step / maxStep) + minWait));
    lastdist:= dist;
  end;
  if (Round(xe) <> Round(xs)) or (Round(ye) <> Round(ys)) then
    MoveMouse(Round(xe), Round(ye));
end;


//TODO: Modularize this to allow for different paths its soo ugly.
(*
MMouse
~~~~~~

.. code-block:: pascal

    procedure MMouse(x, y, rx, ry: integer);

Moves the mouse.

.. note::

    by Benland100, laptop mouse by Hobbit (and nielsie95)

Example:

.. code-block:: pascal

*)
procedure MMouse(x, y, rx, ry: integer);
var
  cx, cy: integer;
  randSpeed: Extended;
  {$IFDEF UseLaptopMouse}
    seg, e, f, g, nx, ny, hypo: Integer;
    a, b, c: Extended;
    Miss: Boolean;
  {$ENDIF}
begin
  GetMousePos(cx, cy);
  {$IFDEF UseLaptopMouse}
    miss := (Random(LMouse_MissChance) = 0);
    e:= 0;
    a:= x - cx;
    b:= y - cy;
    c:= Pow(a,2) + Pow(b,2)
    hypo:= Round(Sqrt(c));
    case hypo of
      0: Exit;
      1..225: seg:=1;
      226..600: seg:= Random(2) + 1;
      601..1800: seg:= random(3) + 2;
      else seg := 5;
    end;
    f := Round( a / seg);
    g := Round( b / seg);
    repeat
      Wait(30 + random(50));
    {Begin: Modified from MMouse by Benland100}
      randSpeed := (random(MouseSpeed) / 2.0 + MouseSpeed) / 10.0;
      if randSpeed = 0.0 then
        randSpeed := 0.1;
      getMousePos(cx,cy);
      nx:= (cx + (f * e)) + random(rx);
      ny:= (cy + (g * e)) + random(ry);
    {End: Modified from MMouse by Benland100}
      if Miss then
      begin
        nx:= nx + RandomRange(rx, rx * 2);
        ny:= ny + RandomRange(ry, ry * 2);
      end;
      WindMouse(cx,cy,nx,ny,11.0,8.0,10.0/randSpeed,12.0/randSpeed,10.0*randSpeed,10.0*randSpeed);
      e:= e + 1;
    until(e = seg);
    GetMousePos(cx, cy);
    if not PointInBox(Point(cx, cy), IntToBox(x, y, x + rx, y + ry)) then
    begin
      Wait(30 + random(30));
      WindMouse(cx,cy,(x + random(rx)),(y + random(ry)),11.0,6.0,10.0/randSpeed,15.0/randSpeed,10.0*randSpeed,10.0*randSpeed);
    end;
  {$ELSE}
    randSpeed:= (random(MouseSpeed) / 2.0 + MouseSpeed) / 10.0;
    if randSpeed = 0.0 then
      randSpeed := 0.1;
    X := x + random(rx);
    Y := y + random(ry);
    WindMouse(cx,cy,x,y,9.0,3.0,10.0/randSpeed,15.0/randSpeed,10.0*randSpeed,10.0*randSpeed);
  {$ENDIF}
end;

(*
HumanClickMouse
~~~~~~~~~~~

.. code-block:: pascal

    procedure HumanClickMouse(mouseButton: TMouseAction);

Clicks the mouse in a human way on the current mouse spot.

.. note::

    by SRL Community

Example:

.. code-block:: pascal

*)
procedure HumanClickMouse(mouseButton: Integer);
var
  a,b,c : integer;
begin
  if ((mouseButton = mouse_move) or (mouseButton = mouse_none)) then Exit; //TODO: perhaps warn?
  GetMousePos(b, c);
  HoldMouse(b, c, mouseButton);
  repeat
    Wait(20 + Random(30));
    a := a + 1;
  until (a > 4);
  GetMousePos(b, c);
  ReleaseMouse(b, c, mouseButton);
  Wait(50+Random(50));
end;

(*
Mouse
~~~~~

.. code-block:: pascal

    procedure Mouse(mousex, mousey, ranx, rany: Integer; mouseButton: Integer);

Moves then clicks mouse.

.. note::

    by Mutant Squirrle, with a small fix by hy71194

Example:

.. code-block:: pascal

*)
procedure Mouse(mousex, mousey, ranx, rany: Integer; mouseButton: Integer);
begin
  MMouse(mousex, mousey, ranx, rany);
  Wait(60 + Random(30));
  HumanClickMouse(mousebutton);
  Wait(50 + Random(50));
end;

(*
MouseBox
~~~~~~~~

.. code-block:: pascal

    procedure MouseBox(x1, y1, x2, y2: Integer; action: Integer);

Moves mouse into a random position in the box. Clicks if told to.

.. note::

    by SKy Scripter and Nielsie95

Example:

.. code-block:: pascal

*)
procedure MouseBox(x1, y1, x2, y2: Integer; action: Integer);
begin
  case action of
    mouse_Move: MMouse(RandomRange(x1, x2), RandomRange(y1, y2), 0, 0);
    mouse_None: begin end;
  else
      Mouse(RandomRange(x1, x2), RandomRange(y1, y2), 0, 0, action);
  end;
end;

(*
MouseBoxEx
~~~~~~~~~~

.. code-block:: pascal

    procedure MouseBoxEx(x1, y1, x2, y2: Integer; Dist,ClickType: Integer);

Does mouse stuff (ClickType), and uses a point that is within Dist from
the closest point between mouse and the box.

.. note::

    by Raymond

Example:

.. code-block:: pascal

*)
procedure MouseBoxPoint(x1, y1, x2, y2: Integer; Dist: Integer; Action: Integer);
var
  PT : TPoint;
begin
  GetMousePos(pt.x,pt.y);
  PT := RandomPointBoxEx(pt,inttobox(x1,y1,x2,y2),Dist);
  case Action of
    mouse_none: ;
    mouse_move : MMouse(pt.x, pt.y, 0, 0);
  else
    Mouse(pt.x, pt.y, 0, 0, action);
  end;
end;

// forward used for organization
function WaitUpTextMulti(S: TStringArray; Time: Integer): Boolean; Forward;

(*
SpiralMouseMulti
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function SpiralMouseMulti(var fx, fy: Integer; x1, y1, x2, y2: Integer; UpText: TStringArray; PpC, aInc: Integer): Boolean;

Spirals the mouse in a circle from the center of the defined box.
It will continue to spiral until it finds a spiral completely outside
the box, always skipping points outside.
Returns the occurance of the first occurance of UpText into fx, fy.
Takes multiple UpTexts as a TStringArray.
PpC: The number of pixels to increase per spiral.
aInc: The number of stops to make around each spiral.
Takes some testing to get the right combination of PpC and aInc.

.. note::

    by Nava2

Example:

.. code-block:: pascal

*)
function SpiralMouseMulti(var fx, fy: Integer; x1, y1, x2, y2: Integer; UpText: TStringArray; PpC, aInc: Integer): Boolean;
var
  aStep, a, rStep, rV, oB: Extended;
  pX, pY, ms: Integer;
  sP: TPoint;
  L2R: Boolean;
begin
  Result := False;
  ms := MouseSpeed;
  MouseSpeed := 25 + RandomRange(-3, 3);
  sP := MiddleTPA([Point(x1, y1), Point(x2, y2)]);
  aStep := Pi / (aInc / 2);
  a := 0;
  rStep := (PpC / aInc);
  L2R := Random(2) = 0;
  //Writeln('aStep: ' + FloatToStr(aStep) + ' rStep ' + FloatToStr(rStep));
  repeat
    rV := rV + rStep;
    pX := Round(rV * cos(a)) + sP.x;
    pY := Round(rV * sin(a)) + sP.y;
    if L2R then
      a := a + aStep
    else
      a := a - aStep;
    if InRange(pX, x1, x2) and InRange(pY, y1, y2) then
    begin
      oB := 0;
      MMouse(pX - 2, pY - 2, 5, 5);
      if WaitUpTextMulti(UpText, 300) then
      begin
        GetMousePos(fx, fy);
        Result := True;
      end;
    end else
      oB := oB + aStep;
  until Result or (oB > (2 * Pi));
  MouseSpeed := ms;
end;

(*
SpiralMouse
~~~~~~~~~~~

.. code-block:: pascal

    function SpiralMouse(var fx, fy: Integer; x1, y1, x2, y2: Integer; UpText: String; PpC, aInc: Integer): Boolean;

Spirals the mouse in a circle from the center of the defined box.
It will continue to spiral until it finds a spiral completely outside
the box, always skipping points outside.
Returns the occurance of the UpText into fx, fy.
PpC: The number of pixels to increase per spiral.
aInc: The number of stops to make around each spiral.
Takes some testing to get the right combination of PpC and aInc.

.. note::

    by Nava2

Example:

.. code-block:: pascal

*)
function SpiralMouse(var fx, fy: Integer; x1, y1, x2, y2: Integer; UpText: String; PpC, aInc: Integer): Boolean;
begin
  Result := SpiralMouseMulti(fx, fy, x1, y1, x2, y2, [UpText], PpC, aInc);
end;

(*
DidClick
~~~~~~~~

.. code-block:: pascal

    function DidClick(Red: Boolean; Time: integer): boolean;

Checks for the X after a click, Times Out after Time.

.. note::

    by Rasta Magician

Example:

.. code-block:: pascal

*)
function DidClick(Red: Boolean; Time: integer): boolean;
var
  TimeOut, x, y, Trash, w, h: integer;
  B: TBox;
begin
  GetMousePos(x, y);
  GetClientDimensions(w, h);
  B := IntToBox(x - 15, y - 15, x + 15, y + 15);
  if B.x1 < 0 then B.x1 := 0;
  if B.y1 < 0 then B.y1 := 0;
  if B.x2 > w then B.x2 := w;
  if B.y2 > h then B.y2 := h;
  TimeOut := GetSystemTime + Time;
  while (GetSystemTime < TimeOut) do
  begin
    With B do
    if FindColor(Trash, Trash, 254, x1, y1, x2, y2) then
    begin
      if Red then
        Result := true
      else
        Result := false;
      exit;
    end;

    With B do
    if FindColor(Trash, Trash, 65278, x1, y1, x2, y2) then
    begin
      if Red then
        Result := false
      else
        Result := True;
      exit;
    end;
    wait(5);
  end;
end;

(*
DidRedClick
~~~~~~~~~~~

.. code-block:: pascal

    function DidRedClick: Boolean;

Checks for the Yellow X after a Click, Times out after 500 ms

.. note::

    by Rasta Magician

Example:

.. code-block:: pascal

*)
function DidRedClick: Boolean;
begin
  Result := DidClick(true, 1000);
end;

(*
DidYellowClick
~~~~~~~~~~~~~~

.. code-block:: pascal


    function DidYellowClick: Boolean;

Checks for the Yellow X after a Click, Times out after 500 ms

.. note::

    by Rasta Magician

Example:

.. code-block:: pascal

*)
function DidYellowClick: Boolean;
begin
  Result := DidClick(false, 1000);
end;

(*
MouseOval
~~~~~~~~~

.. code-block:: pascal

    procedure MouseOval(cx, cy, mx, my, clickType: Integer);

Clicks a random point within an oval shape. The size of the oval is
defined by cx, cy, mx and my and clickType takes same parameters as MouseBox.

.. note::

    by mixster

Example:

.. code-block:: pascal

*)
procedure MouseOval(cx, cy, mx, my, action: Integer);
var
  o: TPoint;
begin
  o.y := Random(my) * ((Random(2) * 2) - 1);
  o.x := Random(Round(Sqrt(Sqr(my) - Sqr(o.y)) * mx / my)) * ((Random(2) * 2) - 1);

  case action of
    mouse_none: ;
    mouse_move: MMouse(cx + o.x, cy + o.y, 0, 0);
    else
      Mouse(cx + o.x, cy + o.y, 0, 0, action);
  end;
end;

(*
MouseCircle
~~~~~~~~~~~

.. code-block:: pascal

    procedure MouseCircle(MidX, MidY, Radius, clickType: Integer);

Same as MouseOval, but clicks within a circle instead.

.. note::

    by mixster

Example:

.. code-block:: pascal

*)
procedure MouseCircle(MidX, MidY, Radius, action: Integer);
begin
  MouseOval(MidX - Radius, MidY - Radius, MidX + Radius, MidY + Radius, action);
end;

(*
DragMouse
~~~~~~~~~

.. code-block:: pascal

    procedure DragMouse(StartX, StartY, SRandX, SRandY, EndX, EndY, ERandX, ERandY: Integer);

Leftclicks StartX, StartY and drags mouse to EndX, EndY.

.. note::

    by Naum

Example:

.. code-block:: pascal

*)
procedure DragMouse(StartX, StartY, SRandX, SRandY, EndX, EndY, ERandX, ERandY: Integer);
begin
  MMouse(StartX, StartY, SRandX, SRandY);
  Wait(150 + Random(20));
  GetMousePos(StartX, StartY);
  HoldMouse(StartX, StartY, mouse_left);
  Wait(250 + Random(320));
  MMouse(EndX, EndY, ERandX, ERandY);
  Wait(250 + Random(120));
  GetMousePos(EndX, EndY);
  ReleaseMouse(EndX, EndY, mouse_left);
end;
