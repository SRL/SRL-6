(*
Math
====

Supporting math functions.

*)

(*
BoxClosestPoint
~~~~~~~~~~~~~~~

.. code-block:: pascal

    function BoxClosestPoint(p : TPoint; b : TBox) : TPoint;

There are 'nine' places where the point can be (relative to a box)..
The middle, 5, is the box itself.
The rest looks like this:

|    1 | 2 | 3
|    ---------
|    4 | 5 | 6
|    ---------
|    7 | 8 | 9

Place 9 means it's past the b.x2, and past the b.y2,
therefore the smallest dist between the point and the box is the
distance between the point and the box's x2,y2 coordinate.

.. note::

    by Raym0nd

Example:

.. code-block:: pascal

*)

function BoxClosestPoint(p : TPoint; b : TBox) : TPoint;
var
  column,row,place : integer;
begin
  result := Point(-1,-1);
  if p.x < B.X1 then
    Column := 1
  else if p.x > b.x2 then
    Column := 3
  else
    Column := 2;
  if p.y < b.y1 then
    row := 1
  else if p.y > b.y2 then
    row := 3
  else
    row := 2;
  place := (row-1)* 3 + column;
  case Place of
    1 : Result := Point(b.x1,b.y1);
    2 : Result := Point(p.x,b.y1);
    3 : Result := point(b.x2,b.y1);
    4 : Result := point(b.x1,p.y);
    5 : Result := p;
    6 : Result := point(b.x2,p.y);
    7 : Result := point(b.x1,b.y2);
    8 : Result := point(p.x,b.y2);
    9 : Result := point(b.x2,b.y2);
  end;
end;

(*
RandomPointBoxEx
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function RandomPointBoxEx(p : TPoint; b : TBox; extraDist : integer) : TPoint;

Returns a point in the box which is 'aimed' near the TPoint given..
Basically it only allows points that lay within the extradist from
the closest point to be returned.
Practical example:

    Inventory Box.. When your mouse is on the right of an inv item,
    you don't want to click on the left of the box.

.. note::

    by Raym0nd

Example:

.. code-block:: pascal

*)
function RandomPointBoxEx(p : TPoint; b : TBox; extraDist : integer) : TPoint;
var
  TPA : TPointArray;
  Dist : integer;
  ClosestPoint : TPoint;
begin
  if PointInBox(p,b) then
  begin
    result := p;
    exit;
  end;
  ClosestPoint := BoxClosestPoint(p,b);
  Dist := Distance(p.x,p.y,ClosestPoint.x,ClosestPoint.y);
  TPA := TPAFromBox(b);
  FilterPointsDist(TPA,0,dist+extraDist,p.x,p.y);
  if length(TPA) < 1 then
    result := point(RandomRange(b.X1,b.x2),RandomRange(b.y1,b.y2))
  else
    result := TPA[random(length(tpa))];
end;

(*
GetSplitPt
~~~~~~~~~~

.. code-block:: pascal

    function GetSplinePt(Points: TPointArray; Theta: Extended): TPoint;

Returns the point on a spline, defined by control points Points, at Theta

.. note::

    by BenLand100 

Example:

.. code-block:: pascal

*)
function GetSplinePt(Points: TPointArray; Theta: Extended): TPoint;
var
  i, n: Integer;
  XTemp, YTemp: Extended;
begin
  n := GetArrayLength(Points) - 1;
  for i := 0 to n do
  begin
    XTemp := XTemp + (BinCoe(n, i) * Points[i].x * Pow((1 - Theta), n - i) *
      Pow(Theta, i));
    YTemp := YTemp + (BinCoe(n, i) * Points[i].y * Pow((1 - Theta), n - i) *
      Pow(Theta, i));
  end;
  Result.x := Round(XTemp);
  Result.y := Round(YTemp);
end;

(*
MakeSplinePath
~~~~~~~~~~~~~~

.. code-block:: pascal

    function MakeSplinePath(Points: TPointArray; ThetaInc: Extended): TPointArray;

Returns a spline, defined by control points Points, incrementing theta by
ThetaInc

.. note::

    by BenLand100 

Example:

.. code-block:: pascal

*)
function MakeSplinePath(Points: TPointArray; ThetaInc: Extended): TPointArray;
var
  i: Integer;
  t: Extended;
  temp, last: TPoint;
  done: Boolean;
begin
  repeat
    if t >= 1 then
    begin
      t := 1;
      done := True;
    end;
    temp := GetSplinePt(Points, t);
    if ((temp.x <> last.x) and (temp.y <> last.y)) then
    begin
      i := i + 1;
      SetArrayLength(Result, i);
      Result[i - 1] := temp;
      last := temp;
    end;
    t := t + ThetaInc;
  until (done)
end;

(*
MidPoints
~~~~~~~~~

.. code-block:: pascal

    function MidPoints(Path: TPointArray; MaxDist: Integer): TPointArray;

Adds midpoints to Path so no distance on it is greater than MaxDist

.. note::

    by BenLand100 

Example:

.. code-block:: pascal

*)
function MidPoints(Path: TPointArray; MaxDist: Integer): TPointArray;
var
  i, c: Integer;
  last: TPoint;
  done: Boolean;
begin
  if (getarraylength(path) > 0) then
  begin
    repeat
      last := Path[0];
      done := True;
      for i := 1 to GetArrayLength(Path) - 1 do
      begin
        if Sqrt(Pow((Path[i].x - last.x), 2) + Pow((Path[i].y - last.y), 2)) >
          MaxDist then
        begin
          done := False;
          SetArrayLength(Path, GetArrayLength(Path) + 1);
          for c := GetArrayLength(Path) - 1 downto i + 1 do
          begin
            Path[c] := Path[c - 1];
          end;
          Path[i].x := Round((last.x + Path[i + 1].x) / 2);
          Path[i].y := Round((last.y + Path[i + 1].y) / 2);
        end;
        last := Path[i];
      end;
    until (done);
  end;
  Result := Path;
end;

(*
InAbstractBox
~~~~~~~~~~~~~

.. code-block:: pascal

    function InAbstractBox(x1, y1, x2, y2, x3, y3, x4, y4: Integer; x, y: Integer): Boolean;

Returns true if point x, y is in an abstract box defined by 
*x1, y1, x2, y2, x3, y3, x4, y4*.

| x1, y1     x2, y2
|    +--------+
|     \       /
|      \     /
|       +--+
| x4, y4    x3, y3

.. note::

    by BenLand100 

Example:

.. code-block:: pascal

*)
function InAbstractBox(x1, y1, x2, y2, x3, y3, x4, y4: Integer; x, y: Integer):
  Boolean;
var
  U, D, R, L: Boolean;
  UB, DB, LB, RB, UM, DM, LM, RM: Extended;
begin
  UM := (-y1 - -y2) div (x1 - x2);
  DM := (-y4 - -y3) div (x4 - x3);
  if x1 - x4 <> 0 then
  begin
    LM := (-y1 - -y4) div (x1 - x4);
  end else
  begin
    LM := Pi;
  end;
  if x2 - x3 <> 0 then
  begin
    RM := (-y2 - -y3) div (x2 - x3);
  end else
  begin
    RM := Pi;
  end;
  UB := -(UM * x1) + -y1;
  RB := -(RM * x2) + -y2;
  DB := -(DM * x3) + -y3;
  LB := -(LM * x4) + -y4;
  if (UM * x + UB >= -y) then U := True;
  if (DM * x + DB <= -y) then D := True;
  if (RM <> Pi) and (RM >= 0) and (RM * x + RB <= -y) then R := True;
  if (RM <> Pi) and (RM < 0) and (RM * x + RB >= -y) then R := True;
  if (RM = Pi) and (x < x2) then R := True;
  if (LM <> Pi) and (LM >= 0) and (LM * x + LB >= -y) then L := True;
  if (LM <> Pi) and (LM < 0) and (LM * x + LB <= -y) then L := True;
  if (LM = Pi) and (x > x1) then L := True;
  if U and D and L and R then Result := True;
end;

{*******************************************************************************
function inAngle(Origin: TPoint; Angle1, Angle2, Radius1, Radius2: Extended; X, Y: Integer): Boolean;
By: BenLand100
Description: Returns True if X and Y fall within Radius1 to Radius2 and Angle1 to Angle2
Note1: EVERYTHING IS RELATIVE TO ORIGIN!!!
Note2: This checks in the smallest segment of the circle formed by Angle1 and Angle 2
Example: (Assume the origin is 0,0)
 inAngle(0, 90, 5, 10, origin, 5, 5) = true;
 inAngle(0, 90, 0, 5, origin, 5, 5) = false;
 inAngle(90, 0, 5, 10, origin, 5, 5) = false;
*******************************************************************************}
{
function inAngle(Origin: TPoint; Angle1, Angle2, Radius1, Radius2: Extended; x,
  y: Integer): Boolean;
var
  PTemp: PPoint;
  OTemp: TPoint;
  MinAngle, MaxAngle, MinRadius, MaxRadius: Extended;
begin
  Angle1 := FixD(Angle1);
  Angle2 := FixD(Angle2);
  MinAngle := Angle1;
  if Angle1 > Angle2 then MinAngle := Angle2;
  MaxAngle := Angle1;
  if Angle1 < Angle2 then MaxAngle := Angle2;
  MinRadius := Radius1;
  if Radius1 > Radius2 then MinRadius := Radius2;
  MaxRadius := Radius1;
  if Radius1 < Radius2 then MaxRadius := Radius2;
  OTemp.x := x;
  OTemp.y := y;
  PTemp := ToPolarOffset(OTemp, Origin);
  if (PTemp.R >= MinRadius) and (PTemp.R <= MaxRadius) then
    if (PTemp.T >= MinAngle) and (PTemp.T <= MaxAngle) then
      Result := True;
end;
}

(*
MMToMS
~~~~~~

.. code-block:: pascal

    function MMToMS(MM: TPoint): TPoint;

Turns a Minimap point into a close MS point.

.. note::

    by N1ke!

Example:

.. code-block:: pascal

*)
function MMToMS(MM: TPoint): TPoint;
var
  X, Y: Integer;
  Dis: TPoint;
begin
  X := ((MM.X - 647));
  Y := ((MM.Y - 84));

  Dis := Point( (MMCX - MM.X)*-1, (MMCY - MM.Y)*-1);
  Result := Point(Round((259.5 + X)+ Dis.X*10), Round((170.0 + Y)+ Dis.Y*6.5));

  If not IntInBox(Result.X, Result.Y, IntToBox(MSX1, MSY1, MSX2, MSY2)) then
    Result := Point(-1, -1);
end;

(*
DeleteValueInStrArray
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInStrArray(var Arr: TStringArray; ValuePosition: Integer);

Deletes value with ValuePosition in "Arr" string array.

.. note::

    by EvilChicken!

Example:

.. code-block:: pascal

*)
procedure DeleteValueInStrArray(var Arr: TStringArray; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    Swap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;

(*
DeleteValueInIntArray
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInIntArray(var Arr: TIntegerArray; ValuePosition: Integer);

Deletes value with ValuePosition in "Arr" Integer array.

.. note::

    by EvilChicken!

Example:

.. code-block:: pascal

*)
procedure DeleteValueInIntArray(var Arr: TIntegerArray; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    Swap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;

(*
DeleteValueInFloatArray
~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInFloatArray(var Arr: TExtendedArray; ValuePosition: Integer);


Deletes value with ValuePosition in "Arr" Float array.

.. note::

    by EvilChicken!

Example:

.. code-block:: pascal

*)
procedure DeleteValueInFloatArray(var Arr: TExtendedArray; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    Swap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;

(*
DeleteValueInBoolArray
~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInBoolArray(var Arr: TBooleanArray; ValuePosition: Integer);

Deletes value with ValuePosition in "Arr" Boolean array.

.. note::

    by EvilChicken!

Example:

.. code-block:: pascal

*)
procedure DeleteValueInBoolArray(var Arr: array of boolean; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    Swap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;

(*
DeleteValueInTPA
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInTPA(var Arr: TPointArray; ValuePosition: Integer);

Deletes value with ValuePosition in "Arr" TPointArray.

.. note::

    by EvilChicken! & Coh3n

Example:

.. code-block:: pascal

*)
procedure DeleteValueInTPA(var Arr: TPointArray; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    tSwap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;

(*
DeleteValueInATPA
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure DeleteValueInATPA(var Arr: T2DPointArray; ValuePosition: Integer);

Deletes value with ValuePosition in "Arr" T2DPointArray.

.. note::

    by EvilChicken! & Coh3n

Example:

.. code-block:: pascal

*)
procedure DeleteValueInATPA(var Arr: T2DPointArray; ValuePosition: Integer);
var
  ArrLen, I: Integer;
begin
  ArrLen := High(Arr);
  for I := ValuePosition to ArrLen - 1 do
    tpaSwap(Arr[I], Arr[I + 1]);
  SetArrayLength(Arr, ArrLen);
end;



(*
MiddleBox
~~~~~~~~~

.. code-block:: pascal

    function MiddleBox(b : TBox) : TPoint;

Returns the middle of the box.

.. note::

    by Raym0nd

Example:

.. code-block:: pascal

*)
function MiddleBox(b : TBox) : TPoint;
begin
  result := point((b.x2+b.x1) div 2,(b.y2+b.y1) div 2);
end;

function InCircle(x, y, mx, my, r: Integer): Boolean;
begin
  Result := (Distance(X, Y, mX, mY) <= R);
end;
// TODO: FIX DOC FOR AMAX

function AMax(Data: TIntegerArray): Integer;
var
  i : integer;
begin;
  if length(data) = 0 then
  begin
    Writeln('cannot define result of AMax, input length = 0');
    raiseexception(erCustomError,'AMax input with length of zero');
  end;
  result := data[0];
  for i := high(data) downto 1 do
    result := max(result,data[i]);
end;
